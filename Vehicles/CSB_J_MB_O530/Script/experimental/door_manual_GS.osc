'##############
'Door Script
'##############

'Controls Doors

'(c) 2007-2009 Rüdiger Hülsmann
'(c) 2007-2009 Marcel Kuhnt

'Script Version: 1.1
'Omsi release: 1.0

'Needs:
'- elec

'Revision History:
'- Marcel Kuhnt		13.06.2009	Batterietrennschalter => elec_busbar_main
'- Marcel Kuhnt		21.06.2009	AI-Unterstützung hinzugefügt
'- Rüdiger Hülsmann	24.10.2009	Ansteuerung des Kinderwagenwunsches modifiziert
'- Rüdiger Hülsmann	19.12.2009	Timer für Ausstiegstür jetzt Timegap-basiert, Bugfix: Tür schließt jetzt nur bei elec_busbar=1
'- RÜdiger Hülsmann	22.09.2010	Door light trigger value set to 0.1
'- RÜdiger Hülsmann	07.11.2010	Door speed based on reservoir pressure, air-operated doors made an option
'- Marcel Kuhnt		08.11.2010	AI procedure closing doors debugged
'- Rüdiger Hülsmann	21.11.2010	Front doors only operable when busbar on
'- Rüdiger Hülsmann	23.12.2010	External front door operation included, option: doors only operable with actual stop brake
'- Rüdiger Hülsmann	31.12.2010	Electric door function debugged
'- Rüdiger Hülsmann	05.01.2011	20h-switch
'- Rüdiger Hülsmann	07.01.2011	Backdoor manual switch sound debugged, 20h switch depending on bus type
'- Rüdiger Hülsmann	19.01.2011	Electric door support
'- Rüdiger Hülsmann	19.01.2011	Electric door support
'- Morozov Maxim	----------	Removed automatical back door. Added ramp animation... etc...
'- Alexey Vasiliev	09.03.2012	Added more doors for Citaro "Private Edition"
'- Ñåðãåé Wizard	14.11.2013	Added automatical back doors. Rework dooraft for citaro 3door
'--------------------------------------------------------------------------------

{trigger:door0_drag}
	(L.L.AI) !
	{if}
		(L.S.mouse_x) -400 / (L.L.doorDragAcc_0) + 1 min 0 max (S.L.doorDragAcc_0)
	{endif}
{end}

{trigger:door1_drag}
	(L.L.AI) !
	{if}
		(L.S.mouse_x) -400 / (L.L.doorDragAcc_1) + 1 min 0 max (S.L.doorDragAcc_1)
	{endif}
{end}

{trigger:door2_drag}
	(L.L.AI) !
	{if}
		(L.S.mouse_x) -400 / (L.L.doorDragAcc_2) + 1 min 0 max (S.L.doorDragAcc_2)
	{endif}
{end}

{trigger:door3_drag}
	(L.L.AI) !
	{if}
		(L.S.mouse_x) -400 / (L.L.doorDragAcc_3) + 1 min 0 max (S.L.doorDragAcc_3)
	{endif}
{end}

{trigger:int_haltewunsch}
	1 (S.L.haltewunsch)
{end}

{trigger:door_haltewunsch}
	1 (S.L.haltewunsch) (S.L.door_haltewunsch_knopfdruck)
{end}

{trigger:door_haltewunsch_off}
	0 (S.L.door_haltewunsch_knopfdruck)
{end}

{trigger:door_kinderwagenwunsch}
	1 (S.L.door_kinderwagenwunsch) (S.L.haltewunsch)
{end}

{macro:Door_Init}
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.fdoor_acc) + (S.L.doorAcc_0)
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.fdoor_acc) + (S.L.doorAcc_1)
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.bdoor_acc) + (S.L.doorAcc_2)
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.bdoor_acc) + (S.L.doorAcc_3)
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.bdoor_acc) + (S.L.doorAcc_4)
	1000 random 500 / 1 - (C.L.door_acc_var) * (C.L.bdoor_acc) + (S.L.doorAcc_5)

	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.fdoor_maxspeed) + (S.L.doorMaxSpeed_0_norm)
	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.fdoor_maxspeed) + (S.L.doorMaxSpeed_1_norm)
	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.bdoor_maxspeed) + (S.L.doorMaxSpeed_2_norm)
	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.bdoor_maxspeed) + (S.L.doorMaxSpeed_3_norm)
	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.bdoor_maxspeed) + (S.L.doorMaxSpeed_4_norm)
	1000 random 500 / 1 - (C.L.door_maxspeed_var) * (C.L.bdoor_maxspeed) + (S.L.doorMaxSpeed_5_norm)

	(C.L.door_refl) (S.L.doorRefl_0)
	(C.L.door_refl) (S.L.doorRefl_1)
	(C.L.door_refl) (S.L.doorRefl_2)
	(C.L.door_refl) (S.L.doorRefl_3)
	(C.L.door_refl) (S.L.doorRefl_4)
	(C.L.door_refl) (S.L.doorRefl_5)

	(C.L.tuerverzoegerung) (S.L.doortimer23) (S.L.doortimer45)
{end}

{macro:Door_Frame}
	(M.L.Door0_Calc)
	(M.L.Door1_Calc)
	(M.L.Door2_Calc)
	(M.L.Door3_Calc)
	(M.L.Door4_Calc)
	(M.L.Door5_Calc)

	(L.L.elec_busbar_avail)
	{if}
		(L.L.door_0) (L.L.doorTarget_0) || (L.L.doorSpeed_0) ||
		{if}
			1 (S.L.door_0_opening_status)
			0 (S.L.door_0_opening_status_post_close_timer)
		{else}
			(L.L.door_0_opening_status) 1 =
			{if}
				-1 (S.L.door_0_opening_status)
			{endif}
			(L.L.door_0_opening_status) -1 =
			{if}
				(L.L.door_0_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_0_opening_status_post_close_timer) 10 =
				{if}
					0 (S.L.door_0_opening_status) (S.L.door_0_opening_status_post_close_timer)
				{endif}
			{endif}
		{endif}

		(L.L.door_1) (L.L.doorTarget_1) || (L.L.doorSpeed_1) ||
		{if}
			1 (S.L.door_1_opening_status)
			0 (S.L.door_1_opening_status_post_close_timer)
		{else}
			(L.L.door_1_opening_status) 1 =
			{if}
				-1 (S.L.door_1_opening_status)
			{endif}
			(L.L.door_1_opening_status) -1 =
			{if}
				(L.L.door_1_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_1_opening_status_post_close_timer) 10 =
				{if}
					0 (S.L.door_1_opening_status) (S.L.door_1_opening_status_post_close_timer)
				{endif}
			{endif}
		{endif}

		(L.L.door_0) (L.L.door_1) || (L.L.doorTarget_0) || (L.L.doorTarget_1) || (L.L.doorSpeed_0) || (L.L.doorSpeed_1) ||
		{if}
			1 (S.L.door_01_opening_status)
			0 (S.L.door_01_opening_status_post_close_timer)
		{else}
			(L.L.door_01_opening_status) 1 =
			{if}
				-1 (S.L.door_01_opening_status)
			{endif}
			(L.L.door_01_opening_status) -1 =
			{if}
				(L.L.door_01_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_01_opening_status_post_close_timer) 10 =
				{if}
					0 (S.L.door_01_opening_status) (S.L.door_01_opening_status_post_close_timer)
				{endif}
			{endif}
		{endif}

		(L.L.door_2) (L.L.door_3) || (L.L.doorTarget_23) || (L.L.doorSpeed_2) || (L.L.doorSpeed_3) ||
		{if}
			1 (S.L.door_23_opening_status)
			0 (S.L.door_23_opening_status_post_close_timer)
		{else}
			(L.L.door_23_opening_status) 1 =
			{if}
				-1 (S.L.door_23_opening_status)
			{endif}
			(L.L.door_23_opening_status) -1 =
			{if}
				(L.L.door_23_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_23_opening_status_post_close_timer) 10 =
				{if}
					0 (S.L.door_23_opening_status) (S.L.door_23_opening_status_post_close_timer)
				{endif}
			{endif}
		{endif}

		(C.L.number_of_doors) 2 >
		{if}
			(L.L.door_4) (L.L.door_5) || (L.L.doorTarget_45) || (L.L.doorSpeed_4) || (L.L.doorSpeed_5) ||
			{if}
				1 (S.L.door_45_opening_status)
				0 (S.L.door_45_opening_status_post_close_timer)
			{else}
				(L.L.door_45_opening_status) 1 =
				{if}
					-1 (S.L.door_45_opening_status)
				{endif}
				(L.L.door_45_opening_status) -1 =
				{if}
					(L.L.door_45_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_45_opening_status_post_close_timer) 10 =
					{if}
						0 (S.L.door_45_opening_status) (S.L.door_45_opening_status_post_close_timer)
					{endif}
				{endif}
			{endif}

			(C.L.number_of_doors) 3 >
			{if}
				(L.L.door_6) (L.L.door_7) || (L.L.doorTarget_67) || (L.L.doorSpeed_6) || (L.L.doorSpeed_7) ||
				{if}
					1 (S.L.door_67_opening_status)
					0 (S.L.door_67_opening_status_post_close_timer)
				{else}
					(L.L.door_67_opening_status) 1 =
					{if}
						-1 (S.L.door_67_opening_status)
					{endif}
					(L.L.door_67_opening_status) -1 =
					{if}
						(L.L.door_67_opening_status_post_close_timer) (L.S.Timegap) + 10 min (S.L.door_67_opening_status_post_close_timer) 10 =
						{if}
							0 (S.L.door_67_opening_status) (S.L.door_67_opening_status_post_close_timer)
						{endif}
					{endif}
				{endif}
			{endif}
		{endif}
	{else}
		0 (S.L.door_01_opening_status) (S.L.door_01_opening_status_post_close_timer) (S.L.door_23_opening_status)
			(S.L.door_23_opening_status_post_close_timer) (S.L.door_45_opening_status) (S.L.door_45_opening_status_post_close_timer)
			(S.L.door_67_opening_status) (S.L.door_67_opening_status_post_close_timer)
	{endif}

	(L.L.door_cti_front_door_decoupled_from_stop_brake)
	{if}
		1 (S.L.Freigabe)
	{else}
		(L.L.door_01_opening_status) 0 = (L.L.door_01_opening_status_post_close_timer) 0.5 > || (S.L.Freigabe)
	{endif}

	(M.L.DoorAftCalc)

	(L.L.cp_fahrertuer_pos) s1
	(L.L.door_0) 0.9 > l1 0.05 < && (S.L.PAX_Entry0_Open)
	(L.L.door_1) 0.9 > l1 0.5 < && (S.L.PAX_Entry1_Open)
	(L.L.PAX_Exit0_Req) ! (L.L.PAX_Exit1_Req) ! && s1
	(L.L.door_2) 0.9 > (S.L.PAX_Exit0_Open) l1 && (S.L.PAX_Entry2_Open)
	(L.L.door_3) 0.9 > (S.L.PAX_Exit1_Open) l1 && (S.L.PAX_Entry3_Open)
	(L.L.PAX_Exit2_Req) ! (L.L.PAX_Exit3_Req) ! && s1
	(L.L.door_4) 0.9 > (S.L.PAX_Exit2_Open) l1 && (S.L.PAX_Entry4_Open)
	(L.L.door_5) 0.9 > (S.L.PAX_Exit3_Open) l1 && (S.L.PAX_Entry5_Open)

'	Nothahn-Rebelüftung

	(L.L.door_01_nothahn_sw)
	{if}
		1 (S.L.door_01_nothahn)
	{endif}
	(L.L.door_01_nothahn)
	(L.L.door_01_nothahn_sw) ! &&
	{if}
		(L.L.door_0_soll) s0
		(L.L.doorDragAcc_0) s1
		1 s2
		1 s3
		(M.L.door_traegheit)
		(S.L.doorDragAcc_0)

		(L.L.door_1_soll) s0
		(L.L.doorDragAcc_1) s1
		1 s2
		1 s3
		(M.L.door_traegheit)
		(S.L.doorDragAcc_1)

		(L.L.cockpit_tuertaster1)
		{if}
			0 (S.L.door_01_nothahn) (S.L.doorTarget_0) (S.L.doorTarget_1)
			(L.L.door_0) 0.5 >
			(L.L.door_1) 0.5 > ||
			{if}
				(T.L.ev_doortriggerclose_0)
				(T.L.ev_doortriggerclose_1)
			{endif}
		{endif}
	{endif}

	(L.L.door_23_nothahn_sw)
	{if}
		1 (S.L.door_23_nothahn)
	{endif}
	(L.L.door_23_nothahn)
	(L.L.door_23_nothahn_sw) ! &&
	{if}
		(L.L.door_2_soll) s0
		(L.L.doorDragAcc_2) s1
		1 s2
		1 s3
		(M.L.door_traegheit)
		(S.L.doorDragAcc_2)

		(L.L.door_3_soll) s0
		(L.L.doorDragAcc_3) s1
		1 s2
		1 s3
		(M.L.door_traegheit)
		(S.L.doorDragAcc_3)

		(L.L.cockpit_tuertaster2)
		{if}
			0 (S.L.door_23_nothahn) (S.L.doorTarget_23)
			(L.L.door_2) 0.5 >
			{if}
				(T.L.ev_doortriggerclose_2)
			{endif}
		{endif}
	{endif}

'    Türsteuerung:
' Tür 1
    1 s2
    (L.L.door_0) 0.5 < (L.L.door_1) 0.5 < && (L.L.doorTarget_0) ! && (L.L.doorTarget_1) ! &&
    {if}
        (L.L.cp_tuersperre_sw) s0 !
        {if}
            3 (S.L.doorlock_mode)
        {else}
            l0 0 >
            {if}
                1 (S.L.doorlock_mode)
            {else}
                2 (S.L.doorlock_mode)
            {endif}
        {endif}
    {else}
        (L.L.door_0) 0.5 >= (L.L.doorTarget_0) && s0 (L.L.door_1) 0.5 >= (L.L.doorTarget_1) && s1 &&
        {if}
            -3 (S.L.doorlock_mode)
        {else}
            (L.L.door_1) ! (L.L.doorTarget_1) ! && l0 &&
            {if}
                -1 (S.L.doorlock_mode)
            {else}
                (L.L.door_0) ! (L.L.doorTarget_0) ! && l1 &&
                {if}
                    -2 (S.L.doorlock_mode)
                {else}
                    0 s2
                {endif}
            {endif}
        {endif}
    {endif}

    (L.L.elec_busbar_main) (C.L.elec_busbar_minV) > (L.L.bremse_p_tank04) 550000 > && (L.L.door_01_nothahn_sw) ! &&
		l2 &&
    {if}
        (L.L.doorlock_mode) s0 0 > (L.L.cockpit_tuertaster1) &&
        {if}
            (L.L.velocity) abs (C.L.tueroeffnung) <
            {if}
				(L.L.Door_Error_Count) 1 + (S.L.Door_Error_Count)
				(C.L.Tuerfehlertoleranz) (L.L.wearlifespan) * (L.L.Door_Error_Count) > (L.L.AI) ||
				{if}
					l0 3 =
					{if}
						1 (S.L.doorTarget_0) (S.L.doorTarget_1)
						(T.L.ev_doortriggeropen_0)
						(T.L.ev_doortriggeropen_1)
					{else}
						l0 1 =
						{if}
							1 (S.L.doorTarget_0)
							(T.L.ev_doortriggeropen_0)
						{else}
							1 (S.L.doorTarget_1)
							(T.L.ev_doortriggeropen_1)
						{endif}
					{endif}
					(L.L.AI) !
					{if}
						(L.L.bremse_p_Tank04) 100000 - 0.96 * 100000 + (S.L.bremse_p_Tank04)
					{endif}
					(L.L.door_cti_front_door_decoupled_from_stop_brake) !
						(L.L.velocity) abs (C.L.Door_Bremse_Halte_MaxKMH) < && (L.L.cp_not_aus_sw) ! &&
					{if}
						(M.L.door_set_stop_brake)
					{endif}
				{endif}
            {endif}
        {else}
            (L.L.doorlock_mode) s0 0 < (L.L.cockpit_tuertaster1) &&
            {if}
				(L.L.Door_Error_Count) 1 + (S.L.Door_Error_Count)
				(C.L.Tuerfehlertoleranz) (L.L.wearlifespan) * (L.L.Door_Error_Count) > (L.L.AI) ||
				{if}
					l0 -3 =
					{if}
						0 (S.L.doorTarget_0) (S.L.doorTarget_1)
						(T.L.ev_doortriggerclose_0)
						(T.L.ev_doortriggerclose_1)
					{else}
						l0 -1 =
						{if}
							0 (S.L.doorTarget_0)
							(T.L.ev_doortriggerclose_0)
						{else}
							0 (S.L.doorTarget_1)
							(T.L.ev_doortriggerclose_1)
						{endif}
                    {endif}
					(L.L.AI) !
					{if}
						(L.L.bremse_p_Tank04) 100000 - 0.96 * 100000 + (S.L.bremse_p_Tank04)
					{endif}
                {endif}
            {endif}
        {endif}
    {endif}

'Tür 2
    (L.L.elec_busbar_main) (C.L.elec_busbar_minV) > (L.L.bremse_p_tank04) 550000 > && (L.L.door_23_nothahn_sw) ! &&
    {if}
        (L.L.door_2) 0.5 < (L.L.doorTarget_23_delayed) ! && (L.L.door_3) 0.5 < && (L.L.cockpit_tuertaster2) &&
        {if}
            (L.L.velocity) abs (C.L.tueroeffnung) <
            {if}
				(L.L.Door2_Error_Count) 1 + (S.L.Door2_Error_Count)
				(C.L.Tuerfehlertoleranz) (L.L.wearlifespan) * (L.L.Door2_Error_Count) > (L.L.AI) ||
				{if}
					1 (S.L.doorTarget_23_delayed)
					(T.L.ev_doortriggeropen_2)
					(L.L.velocity) abs (C.L.Door_Bremse_Halte_MaxKMH) < (L.L.cp_not_aus_sw) ! &&
					{if}
						(M.L.door_set_stop_brake)
					{endif}
					(L.L.AI) !
					{if}
						(L.L.bremse_p_Tank04) 100000 - 0.96 * 100000 + (S.L.bremse_p_Tank04)
					{endif}
				{endif}
            {endif}
        {else}
            (L.L.door_2) 0.5 >= (L.L.doorTarget_23_delayed) && (L.L.door_3) 0.5 >= && (L.L.cockpit_tuertaster2) &&
            {if}
				(L.L.Door2_Error_Count) 1 + (S.L.Door2_Error_Count)
				(C.L.Tuerfehlertoleranz) (L.L.wearlifespan) * (L.L.Door2_Error_Count) > (L.L.AI) ||
				{if}
					0 (S.L.doorTarget_23_delayed)
					(C.L.tuerverzoegerung) 0 > (L.L.door_2) 1 = && (L.L.door_3) 1 = &&
					{if}
						(T.L.ev_doorwarn)
					{endif}
					(L.L.AI) !
					{if}
						(L.L.bremse_p_Tank04) 100000 - 0.96 * 100000 + (S.L.bremse_p_Tank04)
					{endif}
				{endif}
            {endif}
        {endif}
    {endif}

    (L.L.doorTarget_23_delayed) !
    {if}
        (L.L.door_2) 1 =
        (L.L.door_3) 1 =
        {if}
            (L.L.doortimer23) (L.S.Timegap) + (C.L.tuerverzoegerung) min (S.L.doortimer23)
            (C.L.tuerverzoegerung) =
            {if}
                0 (S.L.doorTarget_23)
            {endif}
        {else}
            0 (S.L.doorTarget_23)
        {endif}
    {else}
        1 (S.L.doorTarget_23)
        0 (S.L.doortimer23)
    {endif}

'	"Dauerfeuer"-Haltewunsch über Kinderwagenschalter (macht genau dasselbe wie der "trigger:int_haltewunsch"):
	(L.L.door_haltewunsch_knopfdruck)
	{if}
		1 (S.L.haltewunsch)
	{endif}

'	Setzen der Haltestellenbremse erst bei Stillstand des Busses:
	(L.L.elec_busbar_main) (C.L.elec_busbar_minV) >
	{if}
		(L.L.bremse_halte_sw) (L.L.door_20h_sw) || (L.L.Velocity) abs (C.L.Door_Bremse_Halte_MaxKMH) < &&
		{if}
			(M.L.door_set_stop_brake)
		{endif}
	{endif}

'	Türfreigabekriterium

	(L.L.bremse_halte_sw) (L.L.bremse_halte) && (S.L.door_freigabe)

'	Haltewunschlampe:
	(L.L.haltewunsch) (L.L.door_0) 0.2 < && (L.L.door_1) 0.2 < && (L.L.door_2) 0.2 < && (L.L.door_3) 0.2 < &&
		(L.L.door_4) 0.2 < && (L.L.door_5) 0.2 < && (L.L.elec_busbar_main) (C.L.elec_busbar_minV) > &&
	{if}
		(L.L.haltewunschlampe) !
		{if}
			(T.L.ev_door_stop_request_buzzer)
		{endif}
		1 (S.L.haltewunschlampe)
	{else}
		0 (S.L.haltewunschlampe) (S.L.haltewunsch)
	{endif}

'	Kinderwagenwunsch:
	(L.L.door_2) 0.2 > (L.L.door_3) 0.2 > || (L.L.door_4) 0.2 > || (L.L.door_5) 0.2 > || (L.L.elec_busbar_main)
		(C.L.elec_busbar_minV) > &&
	{if}
		0 (S.L.door_kinderwagenwunsch) (S.L.zufall_kinderwagenwunsch)
	{endif}

'	Abfall des Kinderwagenwunsch-Relais bei fehlender Stromversorgung:

	(L.L.elec_busbar_main) (C.L.elec_busbar_minV) <=
	{if}
		0 (S.L.door_kinderwagenwunsch) (S.L.zufall_kinderwagenwunsch)
	{endif}

'	Zufälliges zusätzliches Aufleuchten des Kinderwagenwunsches bei Haltewunsch:

	(L.L.haltewunsch) 1 =
	(L.L.zufall_kinderwagenwunsch) 0 = &&
	(L.S.Time) 72000 < (L.S.Time) 28800 > && &&
	{if}
		10 random (S.L.zufall_kinderwagenwunsch)
		(L.L.zufall_kinderwagenwunsch) 9 >=
		{if}
			1 (S.L.door_kinderwagenwunsch)
		{endif}
	{endif}

'	Druckabhängige Türgeschwindigkeit

	(C.L.door_druckluft)
	{if}
		(L.L.doorMaxSpeed_0_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_0)
		(L.L.doorMaxSpeed_1_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_1)
		(L.L.doorMaxSpeed_2_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_2)
		(L.L.doorMaxSpeed_3_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_3)
		(L.L.doorMaxSpeed_4_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_4)
		(L.L.doorMaxSpeed_5_norm) (L.L.bremse_p_Tank04) 100000 - 850000 / (S.L.doorMaxSpeed_5)
	{endif}
{end}

{macro:Door0_Calc}
	(L.L.door_01_nothahn) !
	{if}
		(C.L.electric_doors)
		{if}
			(L.L.doorTarget_0)
			{if}
				(L.L.door_0) (F.L.door_0_opn_speed) (S.L.doorMaxSpeed_0)
			{else}
				(L.L.AI)
				{if}
					(L.L.door_0) (F.L.door_0_cls_speed_ai)
				{else}
					(L.L.door_0) (F.L.door_0_cls_speed)
				{endif}
				(S.L.doorMaxSpeed_0)
			{endif}

			(C.L.fdoor_acc) (S.L.doorAcc_0)

			(L.L.doorTarget_0) ! (L.L.doorSpeed_0) s0 abs 0.05 > (L.L.door_0) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_0) <
				{if}
					(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * - (S.L.doorSpeed_0)
				{else}
					(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * + (S.L.doorSpeed_0)
				{endif}
				(L.L.door_0) (L.L.doorSpeed_0) (L.S.Timegap) * + (S.L.door_0)
			{else}
				(L.L.doorSpeed_0) abs 0.05 > (L.L.door_0) 1 < || (L.L.doorTarget_0) &&
				{if}
					l0 (L.L.doorMaxSpeed_0) <
					{if}
						(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * + (S.L.doorSpeed_0)
					{else}
						(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * - (S.L.doorSpeed_0)
					{endif}
					(L.L.door_0) (L.L.doorSpeed_0) (L.S.Timegap) * + (S.L.door_0)
				{else}
					0 (S.L.doorSpeed_0)
				{endif}
			{endif}
		{else}
			(L.L.doorTarget_0) ! (L.L.doorSpeed_0) s0 abs 0.05 > (L.L.door_0) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_0) <
				{if}
					(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * - (S.L.doorSpeed_0)
				{endif}
				(L.L.door_0) (L.L.doorSpeed_0) (L.S.Timegap) * + (S.L.door_0)
			{else}
				(L.L.doorSpeed_0) abs 0.05 > (L.L.door_0) 1 < || (L.L.doorTarget_0) &&
				{if}
					l0 (L.L.doorMaxSpeed_0) <
					{if}
						(L.L.doorSpeed_0) (L.L.doorAcc_0) (L.S.Timegap) * + (S.L.doorSpeed_0)
					{endif}
					(L.L.door_0) (L.L.doorSpeed_0) (L.S.Timegap) * + (S.L.door_0)
				{else}
					0 (S.L.doorSpeed_0)
				{endif}
			{endif}
		{endif}

		(L.L.door_0) 1 >
		{if}
			(T.L.ev_doorhitopen_0)
			1 (S.L.door_0)
			(L.L.doorSpeed_0) /-/ (L.L.doorRefl_0) * (S.L.doorSpeed_0)
		{else}
			(L.L.door_0) 0 <
			{if}
				(T.L.ev_doorhitclose_0)
				0 (S.L.door_0)
				(L.L.doorSpeed_0) /-/ (L.L.doorRefl_0) * (S.L.doorSpeed_0)
			{endif}
		{endif}
		(L.L.door_0) (S.L.doorDragAcc_0)
	{else}
		(L.L.doorDragAcc_0) (S.L.door_0)
	{endif}
{end}

{macro:Door1_Calc}
	(L.L.door_01_nothahn) !
	{if}
		(C.L.electric_doors)
		{if}
			(L.L.doorTarget_1)
			{if}
				(L.L.door_1) (F.L.door_1_opn_speed) (S.L.doorMaxSpeed_1)
			{else}
				(L.L.door_1) (F.L.door_1_cls_speed) (S.L.doorMaxSpeed_1)
			{endif}

			(C.L.fdoor_acc) (S.L.doorAcc_1)

			(L.L.doorTarget_1) ! (L.L.doorSpeed_1) s0 abs 0.05 > (L.L.door_1) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_1) <
				{if}
					(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * - (S.L.doorSpeed_1)
				{else}
					(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * + (S.L.doorSpeed_1)
				{endif}
				(L.L.door_1) (L.L.doorSpeed_1) (L.S.Timegap) * + (S.L.door_1)
			{else}
				(L.L.doorSpeed_1) abs 0.05 > (L.L.door_1) 1 < || (L.L.doorTarget_1) &&
				{if}
					l0 (L.L.doorMaxSpeed_1) <
					{if}
						(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * + (S.L.doorSpeed_1)
					{else}
						(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * - (S.L.doorSpeed_1)
					{endif}
					(L.L.door_1) (L.L.doorSpeed_1) (L.S.Timegap) * + (S.L.door_1)
				{else}
					0 (S.L.doorSpeed_1)
				{endif}
			{endif}
		{else}
			(L.L.doorTarget_1) ! (L.L.doorSpeed_1) s0 abs 0.05 > (L.L.door_1) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_1) <
				{if}
					(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * - (S.L.doorSpeed_1)
				{endif}
				(L.L.door_1) (L.L.doorSpeed_1) (L.S.Timegap) * + (S.L.door_1)
			{else}
				(L.L.doorSpeed_1) abs 0.05 > (L.L.door_1) 1 < || (L.L.doorTarget_1) &&
				{if}
					l0 (L.L.doorMaxSpeed_1) <
					{if}
						(L.L.doorSpeed_1) (L.L.doorAcc_1) (L.S.Timegap) * + (S.L.doorSpeed_1)
					{endif}
					(L.L.door_1) (L.L.doorSpeed_1) (L.S.Timegap) * + (S.L.door_1)
				{else}
					0 (S.L.doorSpeed_1)
				{endif}
			{endif}
		{endif}
		(L.L.door_1) 1 >
		{if}
			(T.L.ev_doorhitopen_1)
			1 (S.L.door_1)
			(L.L.doorSpeed_1) /-/ (L.L.doorRefl_1) * (S.L.doorSpeed_1)
		{else}
			(L.L.door_1) 0 <
			{if}
				(T.L.ev_doorhitclose_1)
				0 (S.L.door_1)
				(L.L.doorSpeed_1) /-/ (L.L.doorRefl_1) * (S.L.doorSpeed_1)
			{endif}
		{endif}
		(L.L.door_1) (S.L.doorDragAcc_1)
	{else}
		(L.L.doorDragAcc_1) (S.L.door_1)
	{endif}
{end}

{macro:DoorAftCalc}
	(L.L.bremse_halte)
	{if}
		(L.L.Freigabe) (L.L.pandus_open) ! && (L.L.door_23_opening_status) 0 =
			(L.L.door_23_opening_status_post_close_timer) 0.5 > || && (L.L.door_45_opening_status) 0 =
			(L.L.door_45_opening_status_post_close_timer) 0.5 > || && (L.L.elec_busbar_main)
			(C.L.elec_busbar_minV) <= || (L.L.cp_not_aus_sw) || (L.L.kneel_timer) 0 = && (L.L.cockpit_throttle) 0.1 >
			(L.L.AI) (L.L.velocity) 0.5 > && || && (L.L.bremse_halte_sw) ! &&
		{if}
			(M.L.door_release_stop_brake)
			0 (S.L.doorAftLastOpen) (S.L.doorAftLastOpen1)
		{endif}
	{endif}
{end}

'--------------------------------------------------------------------------------

{macro:Door2_Calc}
	(L.L.door_23_nothahn) !
	{if}
		(C.L.electric_doors)
		{if}
			(L.L.doorTarget_23)
			{if}
				(L.L.door_2) (F.L.door_2_opn_speed) (S.L.doorMaxSpeed_2)
				(L.L.door_2) (F.L.door_2_opn_speed_2) (S.L.door_2f)
			{else}
				(L.L.door_2) (F.L.door_2_cls_speed) (S.L.doorMaxSpeed_2)
				(L.L.door_2) (F.L.door_2_cls_speed_2) (S.L.door_2f)
			{endif}

			(C.L.bdoor_acc) (S.L.doorAcc_2)

			(L.L.doorTarget_23) ! (L.L.doorSpeed_2) s0 abs 0.05 > (L.L.door_2) 0 > || &&
			{if}
				(L.L.trigger_antirepeat23) 1 + (S.L.trigger_antirepeat23)
				(L.L.trigger_antirepeat23) 1 =
					{if}
					(T.L.ev_doortriggerclose_2)
					{endif}
				l0 /-/ (L.L.doorMaxSpeed_2) <
				{if}
					(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * - (S.L.doorSpeed_2)
				{else}
					(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * + (S.L.doorSpeed_2)
				{endif}
				(L.L.door_2) (L.L.doorSpeed_2) (L.S.Timegap) * + (S.L.door_2)
			{else}
				0 (S.L.trigger_antirepeat23)
				(L.L.doorSpeed_2) abs 0.05 > (L.L.door_2) 1 < || (L.L.doorTarget_23) &&
				{if}
					l0 (L.L.doorMaxSpeed_2) <
					{if}
						(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * + (S.L.doorSpeed_2)
					{else}
						(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * - (S.L.doorSpeed_2)
					{endif}
					(L.L.door_2) (L.L.doorSpeed_2) (L.S.Timegap) * + (S.L.door_2)
				{else}
					0 (S.L.doorSpeed_2)
				{endif}
			{endif}
		{else}
			(L.L.doorTarget_23) ! (L.L.doorSpeed_2) s0 abs 0.05 > (L.L.door_2) 0 > || &&
			{if}
				(L.L.trigger_antirepeat23) 1 + (S.L.trigger_antirepeat23)
				(L.L.trigger_antirepeat23) 1 =
				{if}
					(T.L.ev_doortriggerclose_2)
				{endif}
				l0 /-/ (L.L.doorMaxSpeed_2) <
				{if}
					(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * - (S.L.doorSpeed_2)
				{endif}
				(L.L.door_2) (L.L.doorSpeed_2) (L.S.Timegap) * + (S.L.door_2)
			{else}
				0 (S.L.trigger_antirepeat23)
				(L.L.doorSpeed_2) abs 0.05 > (L.L.door_2) 1 < || (L.L.doorTarget_23) &&
				{if}
					l0 (L.L.doorMaxSpeed_2) <
					{if}
						(L.L.doorSpeed_2) (L.L.doorAcc_2) (L.S.Timegap) * + (S.L.doorSpeed_2)
					{endif}
					(L.L.door_2) (L.L.doorSpeed_2) (L.S.Timegap) * + (S.L.door_2)
				{else}
					0 (S.L.doorSpeed_2)
				{endif}
			{endif}
		{endif}

		(L.L.door_2) 1 >
		{if}
			(T.L.ev_doorhitopen_2)
			1 (S.L.door_2)
			(L.L.doorSpeed_2) /-/ (L.L.doorRefl_2) * (S.L.doorSpeed_2)
		{else}
			(L.L.door_2) 0 <
			{if}
				(T.L.ev_doorhitclose_2)
				0 (S.L.door_2)
				(L.L.doorSpeed_2) /-/ (L.L.doorRefl_2) * (S.L.doorSpeed_2)
			{endif}
		{endif}
		(L.L.door_2) (S.L.doorDragAcc_2)
	{else}
		(L.L.doorDragAcc_2) (S.L.door_2)
	{endif}
{end}

{macro:Door3_Calc}
	(L.L.door_23_nothahn) !
	{if}
		(C.L.electric_doors)
		{if}
			(L.L.doorTarget_23)
			{if}
				(L.L.door_3) (F.L.door_3_opn_speed) (S.L.doorMaxSpeed_3)
				(L.L.door_3) (F.L.door_3_opn_speed_2) (S.L.door_3f)
			{else}
				(L.L.door_3) (F.L.door_3_cls_speed) (S.L.doorMaxSpeed_3)
				(L.L.door_3) (F.L.door_3_cls_speed_2) (S.L.door_3f)
			{endif}

			(C.L.bdoor_acc) (S.L.doorAcc_3)

			(L.L.doorTarget_23) ! (L.L.doorSpeed_3) s0 abs 0.05 > (L.L.door_3) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_3) <
				{if}
					(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * - (S.L.doorSpeed_3)
				{else}
					(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * + (S.L.doorSpeed_3)
				{endif}
				(L.L.door_3) (L.L.doorSpeed_3) (L.S.Timegap) * + (S.L.door_3)
			{else}
				(L.L.doorSpeed_3) abs 0.05 > (L.L.door_3) 1 < || (L.L.doorTarget_23) &&
				{if}
					l0 (L.L.doorMaxSpeed_3) <
					{if}
						(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * + (S.L.doorSpeed_3)
					{else}
						(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * - (S.L.doorSpeed_3)
					{endif}
					(L.L.door_3) (L.L.doorSpeed_3) (L.S.Timegap) * + (S.L.door_3)
				{else}
					0 (S.L.doorSpeed_3)
				{endif}
			{endif}
		{else}
			(L.L.doorTarget_23) ! (L.L.doorSpeed_3) s0 abs 0.05 > (L.L.door_3) 0 > || &&
			{if}
				l0 /-/ (L.L.doorMaxSpeed_3) <
				{if}
					(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * - (S.L.doorSpeed_3)
				{endif}
				(L.L.door_3) (L.L.doorSpeed_3) (L.S.Timegap) * + (S.L.door_3)
			{else}
				(L.L.doorSpeed_3) abs 0.05 > (L.L.door_3) 1 < || (L.L.doorTarget_23) &&
				{if}
					l0 (L.L.doorMaxSpeed_3) <
					{if}
						(L.L.doorSpeed_3) (L.L.doorAcc_3) (L.S.Timegap) * + (S.L.doorSpeed_3)
					{endif}
					(L.L.door_3) (L.L.doorSpeed_3) (L.S.Timegap) * + (S.L.door_3)
				{else}
					0 (S.L.doorSpeed_3)
				{endif}
			{endif}
		{endif}

		(L.L.door_3) 1 >
		{if}
			(T.L.ev_doorhitopen_3)
			1 (S.L.door_3)
			(L.L.doorSpeed_3) /-/ (L.L.doorRefl_3) * (S.L.doorSpeed_3)
		{else}
			(L.L.door_3) 0 <
			{if}
				(T.L.ev_doorhitclose_3)
				0 (S.L.door_3)
				(L.L.doorSpeed_3) /-/ (L.L.doorRefl_3) * (S.L.doorSpeed_3)
			{endif}
		{endif}
		(L.L.door_3) (S.L.doorDragAcc_3)
	{else}
		(L.L.doorDragAcc_3) (S.L.door_3)
	{endif}
{end}

{macro:Door4_Calc}
	(C.L.electric_doors)
	{if}
		(L.L.doorTarget_45)
		{if}
			(L.L.door_4) (F.L.door_4_opn_speed) (S.L.doorMaxSpeed_4)
			(L.L.door_4) (F.L.door_4_opn_speed_2) (S.L.door_4f)
		{else}
			(L.L.door_4) (F.L.door_4_cls_speed) (S.L.doorMaxSpeed_4)
			(L.L.door_4) (F.L.door_4_cls_speed_2) (S.L.door_4f)
		{endif}

		(C.L.bdoor_acc) (S.L.doorAcc_4)

		(L.L.doorTarget_45) ! (L.L.doorSpeed_4) s0 abs 0.05 > (L.L.door_4) 0 > || &&
		{if}
			l0 /-/ (L.L.doorMaxSpeed_3) <
			{if}
				(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * - (S.L.doorSpeed_4)
			{else}
				(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * + (S.L.doorSpeed_4)
			{endif}
			(L.L.door_4) (L.L.doorSpeed_4) (L.S.Timegap) * + (S.L.door_4)
		{else}
			(L.L.doorSpeed_4) abs 0.05 > (L.L.door_4) 1 < || (L.L.doorTarget_45) &&
			{if}
				l0 (L.L.doorMaxSpeed_4) <
				{if}
					(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * + (S.L.doorSpeed_4)
				{else}
					(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * - (S.L.doorSpeed_4)
				{endif}
				(L.L.door_4) (L.L.doorSpeed_4) (L.S.Timegap) * + (S.L.door_4)
			{else}
				0 (S.L.doorSpeed_4)
			{endif}
		{endif}
	{else}
		(L.L.doorTarget_45) ! (L.L.doorSpeed_4) s0 abs 0.05 > (L.L.door_4) 0 > || &&
		{if}
		    (L.L.doortimer45) (C.L.tuerverzoegerung) =
		    {if}
			l0 /-/ (L.L.doorMaxSpeed_4) <
			{if}
				(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * - (S.L.doorSpeed_4)
			{endif}
			(L.L.door_4) (L.L.doorSpeed_4) (L.S.Timegap) * + (S.L.door_4)
		    {endif}
		{else}
			(L.L.doorSpeed_4) abs 0.05 > (L.L.door_4) 1 < || (L.L.doorTarget_45) &&
			{if}
				l0 (L.L.doorMaxSpeed_4) <
				{if}
					(L.L.doorSpeed_4) (L.L.doorAcc_4) (L.S.Timegap) * + (S.L.doorSpeed_4)
				{endif}
				(L.L.door_4) (L.L.doorSpeed_4) (L.S.Timegap) * + (S.L.door_4)
			{else}
				0 (S.L.doorSpeed_4)
			{endif}
		{endif}
	{endif}

	(L.L.door_4) 1 >
	{if}
		(T.L.ev_doorhitopen_4)
		1 (S.L.door_4)
		(L.L.doorSpeed_4) /-/ (L.L.doorRefl_4) * (S.L.doorSpeed_4)
	{else}
		(L.L.door_4) 0 <
		{if}
			(T.L.ev_doorhitclose_4)
			0 (S.L.door_4)
			(L.L.doorSpeed_4) /-/ (L.L.doorRefl_4) * (S.L.doorSpeed_4)
		{endif}
	{endif}
{end}

{macro:Door5_Calc}
	(C.L.electric_doors)
	{if}
		(L.L.doorTarget_45)
		{if}
			(L.L.door_5) (F.L.door_5_opn_speed) (S.L.doorMaxSpeed_5)
			(L.L.door_5) (F.L.door_5_opn_speed_2) (S.L.door_5f)
		{else}
			(L.L.door_5) (F.L.door_5_cls_speed) (S.L.doorMaxSpeed_5)
			(L.L.door_5) (F.L.door_5_cls_speed_2) (S.L.door_5f)
		{endif}

		(C.L.bdoor_acc) (S.L.doorAcc_5)

		(L.L.doorTarget_45) ! (L.L.doorSpeed_5) s0 abs 0.05 > (L.L.door_5) 0 > || &&
		{if}
			l0 /-/ (L.L.doorMaxSpeed_5) <
			{if}
				(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * - (S.L.doorSpeed_5)
			{else}
				(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * + (S.L.doorSpeed_5)
			{endif}
			(L.L.door_5) (L.L.doorSpeed_5) (L.S.Timegap) * + (S.L.door_5)
		{else}
			(L.L.doorSpeed_5) abs 0.05 > (L.L.door_5) 1 < || (L.L.doorTarget_45) &&
			{if}
				l0 (L.L.doorMaxSpeed_5) <
				{if}
					(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * + (S.L.doorSpeed_5)
				{else}
					(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * - (S.L.doorSpeed_5)
				{endif}
				(L.L.door_5) (L.L.doorSpeed_5) (L.S.Timegap) * + (S.L.door_5)
			{else}
				0 (S.L.doorSpeed_5)
			{endif}
		{endif}
	{else}
		(L.L.doorTarget_45) ! (L.L.doorSpeed_5) s0 abs 0.05 > (L.L.door_5) 0 > || &&
		{if}
		    (L.L.doortimer45) (L.S.Timegap) + (C.L.tuerverzoegerung) min (S.L.doortimer45)
		    (C.L.tuerverzoegerung) =
		    {if}
			l0 /-/ (L.L.doorMaxSpeed_5) <
			{if}
				(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * - (S.L.doorSpeed_5)
			{endif}
			(L.L.door_5) (L.L.doorSpeed_5) (L.S.Timegap) * + (S.L.door_5)
		    {endif}
		{else}
			(L.L.doorSpeed_5) abs 0.05 > (L.L.door_5) 1 < || (L.L.doorTarget_45) &&
			{if}
				l0 (L.L.doorMaxSpeed_5) <
				{if}
					(L.L.doorSpeed_5) (L.L.doorAcc_5) (L.S.Timegap) * + (S.L.doorSpeed_5)
				{endif}
				(L.L.door_5) (L.L.doorSpeed_5) (L.S.Timegap) * + (S.L.door_5)
				0 (S.L.doortimer45)
				0 (S.L.trigger_antirepeat45)
			{else}
				0 (S.L.doorSpeed_5)
			{endif}
		{endif}
		(L.L.doortimer45) (C.L.tuerverzoegerung) =
		(L.L.doortimer45) 0 > &&
		{if}
			(L.L.trigger_antirepeat45) 1 + (S.L.trigger_antirepeat45)
			(L.L.trigger_antirepeat45) 1 =
				{if}
				(T.L.ev_doortriggerclose_5)
				{endif}
		{endif}
	{endif}

	(L.L.door_5) 1 >
	{if}
		(T.L.ev_doorhitopen_5)
		1 (S.L.door_5)
		(L.L.doorSpeed_5) /-/ (L.L.doorRefl_5) * (S.L.doorSpeed_5)
	{else}
		(L.L.door_5) 0 <
		{if}
			(T.L.ev_doorhitclose_5)
			0 (S.L.door_5)
			(L.L.doorSpeed_5) /-/ (L.L.doorRefl_5) * (S.L.doorSpeed_5)
		{endif}
	{endif}
{end}

{macro:door_traegheit}

'Simuliert träges Verhalten (exponentiell)

'Ladekonventionen:
'	Sollwert		l0
'	Istwert			l1
'	Konstante Anlauf	l2 (Einheiten/s)
'	Konstante Ablauf	l3 (Einheiten/s)

'Anlauf oder Auslauf?
	l0 l1 >
	{if}
		l2 (L.S.Timegap) * 1 min -1 max s4
	{else}
		l3 (L.S.Timegap) * 1 min -1 max s4
	{endif}


'Sollwert:
	l0
'Istwert:
	l1
'Beschleunigung:
	- l4 *
'Addition zum Istwert:
	l1 + s1
{end}

{macro:door_set_stop_brake}
	(L.L.bremse_halte) !
	{if}
		1 (S.L.bremse_halte)
		(T.L.ev_haltebremse_an)
	{endif}
{end}

{macro:door_release_stop_brake}
	(L.L.bremse_halte)
	{if}
		0 (S.L.bremse_halte)
		(T.L.ev_haltebremse_aus)
	{endif}
{end}