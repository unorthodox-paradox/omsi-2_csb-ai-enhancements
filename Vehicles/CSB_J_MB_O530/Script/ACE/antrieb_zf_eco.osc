'##############
'Getriebe-Script
'##############

'ZF Friedrichshafen ZF Ecomat II/IV + EcoLife Getriebe

'(c) Based on the Voith Script 2008-2010 Rüdiger Hülsmann
'(c) 2016 Edit by Morphi

'----------------------
'   Init
'----------------------

{macro:antrieb_init}
    (L.L.antrieb_initialized_once) !
    {if}
        -1 (S.L.antrieb_getr_initial_shift_delay_timer)

        1 (S.L.antrieb_getr_gangwahl) (S.L.antrieb_getr_gangvorwahl)
            (S.L.antrieb_getr_aktugang) (S.L.antrieb_retarderstufe)

        0 (S.L.antrieb_wandler_moment) (S.L.antrieb_wandler_lockup)

        (M.L.antrieb_cfg_reset_overrides)
        (M.L.antrieb_cfg_apply_defaults)
    {endif}

'   Getriebeidentifizierung

    (C.L.gearbox_version) s0 1 =
    {if}
        1
    {else}
        l0 2 =
        {if}
            0
        {else}
            3
        {endif}
    {endif}
    (S.L.gearbox_identifier)

    0 (S.L.antrieb_initialized)
{end}

{macro:antrieb_cfg_reset_overrides}
    -1 (S.L.antrieb_getr_cfg_cti_standstill_non_neutral_transmission_mode_update_requires_braking)
        (S.L.antrieb_getr_cfg_on_demand_standstill_non_neutral_transmission_mode_update_requires_braking)
{end}

{macro:antrieb_cfg_apply_defaults}
    (C.L.antrieb_getr_cfg_default_standstill_non_neutral_transmission_mode_update_requires_braking) s0 0 = l0 1 = ||
    {if}
        l0
    {else}
        1
    {endif}
    (S.L.antrieb_getr_cfg_effective_standstill_non_neutral_transmission_mode_update_requires_braking)
{end}

'----------------------
'   Frame
'----------------------

{macro:antrieb_frame}

    (L.L.antrieb_initialized) !
    {if}
        1 (S.L.antrieb_initialized)
    {else}
        (L.L.antrieb_initialized) 1 =
        {if}
            (L.L.antrieb_initialized_once) !
            {if}
                1 (S.L.antrieb_initialized_once)
            {endif}
            2 (S.L.antrieb_initialized)
        {endif}
        (M.L.antrieb_cfg_apply_overrides)
    {endif}

'   Berechnen der Abtriebsdrehzahl
    (L.L.Wheel_RotationSpeed_1_L) (L.L.Wheel_RotationSpeed_1_R) + 2 / (C.L.antrieb_i_achse) * (S.L.antrieb_n_kardanwelle) s0

'   Einlegen der Fahrstufe

    (L.L.antrieb_initialized_once) (L.L.antrieb_failure_general) ! (L.L.AI) || &&
    {if}
        (L.L.antrieb_getr_gangwahl) s1
        (L.L.antrieb_getr_gangvorwahl) s2
        (L.L.cockpit_gangwahl) s3
        (L.L.antrieb_getr_initial_shift_delay_timer) s4

        l2 l3 = !
        {if}
            l1 1 >
            {if}
                l3 1 >
                {if}
                    (L.L.AI) ! l4 -1 = ||
                    {if}
                        (C.L.antrieb_gangwahlzeit) s4
                    {endif}
                {else}
                    l3 1 =
                    {if}
                        (L.L.AI) ! l4 -1 = ||
                        {if}
                            (C.L.antrieb_neutralwahlzeit) s4
                        {endif}
                    {endif}
                {endif}
            {else}
                l1 0 =
                {if}
                    l3 1 =
                    {if}
                        (L.L.AI) ! l4 -1 = ||
                        {if}
                            (C.L.antrieb_neutralwahlzeit) s4
                        {endif}
                    {endif}
                {else}
                    l3 1 >
                    {if}
                        (L.L.velocity) 0.5 > (L.L.cockpit_brake) 0.1 > ||
                            (L.L.antrieb_getr_cfg_effective_standstill_non_neutral_transmission_mode_update_requires_braking) ! ||
                            (L.L.AI) ||
                        {if}
                            (L.L.AI) ! l4 -1 = ||
                            {if}
                                (C.L.antrieb_gangwahlzeit) s4
                            {else}
                                2 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
                            {endif}
                        {endif}
                    {else}
                        l3 0 =
                        {if}
                            (L.L.velocity) 0.5 <= (L.L.velocity) -0.5 < (L.L.cockpit_brake) 0.1 > ||
                                (L.L.antrieb_getr_cfg_effective_standstill_non_neutral_transmission_mode_update_requires_braking) ! || &&
                                (L.L.AI) ||
                            {if}
                                (L.L.AI) ! l4 -1 = ||
                                {if}
                                    (C.L.antrieb_gangwahlzeit) s4
                                {else}
                                    2 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
                                {endif}
                            {endif}
                        {endif}
                    {endif}
                {endif}
            {endif}
            l3 s2
        {endif}

        l4 -1 = ! l2 l1 = ! && (L.L.AI) l2 5 = && (L.L.antrieb_getr_aktugang) 0 = && (L.L.velocity) 0.5 > && s5 ||
        {if}
            l4 0 >
            {if}
                l4 (L.S.Timegap) - 0 max s4
            {endif}
            l4 ! l5 ||
            {if}
                l1 1 = l5 || (L.L.engine_on) &&
                {if}
                    (L.L.engine_n) (S.L.antrieb_rucksoundvol)
                    (M.L.antrieb_getr_schaltmoment_runter) (S.L.antrieb_schaltmoment_soll)

                    l2 1 >
                    {if}
                        (C.L.antrieb_getr_autoSwUpMaxSpd5) l0 <
                        {if}
                            6
                        {else}
                            (C.L.antrieb_getr_autoSwUpMaxSpd4) l0 <
                            {if}
                                5
                            {else}
                                (C.L.antrieb_getr_autoSwUpMaxSpd3) l0 <
                                {if}
                                    4
                                {else}
                                    (C.L.antrieb_getr_autoSwUpMaxSpd2) l0 <
                                    {if}
                                        3
                                    {else}
                                        (C.L.antrieb_getr_autoSwUpMaxSpd1c) l0 <
                                        {if}
                                            2
                                        {else}
                                            1
                                        {endif}
                                    {endif}
                                {endif}
                            {endif}
                        {endif}
                        (S.L.antrieb_getr_aktugang)
                    {else}
                        (L.L.velocity) 5 <
                        {if}
                            1 (S.L.antrieb_getr_aktugang)
                        {endif}
                    {endif}

                    (L.L.antrieb_getr_aktugang) 1 =
                    {if}
                        0 (S.L.antrieb_gear_engaged_timer) (S.L.antrieb_wandler_lockup)
                        (T.L.ev_schaltruck)
                    {endif}

                    l5
                    {if}
                        0 (S.L.nbs_fade)
                    {endif}

                    0 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
                {endif}

                l2 s1
                -1 s4
            {endif}
        {else}
            -1 s4
        {endif}

        l1 (S.L.antrieb_getr_gangwahl)
        l2 (S.L.antrieb_getr_gangvorwahl)
        l4 (S.L.antrieb_getr_initial_shift_delay_timer)
    {else}
        -1 (S.L.antrieb_getr_initial_shift_delay_timer)
    {endif}

'   Gang rausnehmen, wenn Neutral angefordert

    (L.L.antrieb_getr_gangwahl) 1 = s0 (L.L.engine_n) 200 < ||
    {if}
        0 (S.L.antrieb_getr_aktugang) (S.L.antrieb_wandler_lockup)
        l0 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
    {endif}

'   Verhindern, dass der Rückwärtsgang oberhalb von 5 km/h eingelegt werden kann

    (L.L.antrieb_getr_gangvorwahl) 0 = (L.L.velocity) 5 >= &&
    {if}
        1 (S.L.antrieb_getr_gangwahl)
        2 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
    {endif}

'   Prüfen, ob Gang gewechselt wird - aber nur, wenn Getriebe heil ist:

    (L.L.antrieb_failure_general) !
    {if}
        (M.L.antrieb_getr_chkNxtGear)
    {else}
        0 (S.L.r10) (M.L.antrieb_getr_upshift_constraint)
        0 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
    {endif}

    (L.L.antrieb_getr_gangwahl) s0 1 > l0 5 < && l0 1 - (L.L.antrieb_getr_aktugang) < && s0

    (L.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync) ! l0 &&
    {if}
        3 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
    {else}
        (L.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync) 3 = l0 ! &&
        {if}
            0 (S.L.antrieb_getr_gangwahl_cockpit_gangwahl_desync)
        {endif}
    {endif}

    (M.L.antrieb_getr_ratio)
    (M.L.antrieb_M_limiter)

'   Berechnung des aktuellen Motordrehmoments:
    (M.L.engine_moment)

'   Berechnung des dynamischen Schaltmoments

    (L.L.cockpit_throttle) 0 =
    {if}
        1000
    {else}
        2000
    {endif}
    (S.L.schaltzeit)

    (L.L.antrieb_getr_fest) !
    {if}
        (L.L.antrieb_schaltmoment) (L.S.Timegap) (L.L.schaltzeit) * + (L.L.antrieb_schaltmoment_soll) min (S.L.antrieb_schaltmoment)
    {endif}

'   Timer für Schaltverzoegerung

    (L.L.cockpit_throttle) (L.L.antrieb_beschleunigung) 0 > ||
    {if}
        (L.L.schalttimer) (L.S.Timegap) + 5 min
    {else}
        0
    {endif}
    (S.L.schalttimer)

    (L.L.schaltverzoegerung) (L.S.Timegap) + 5 min (S.L.schaltverzoegerung)
    (L.L.schaltverzoegerung_runter) (L.S.Timegap) + 5 min (S.L.schaltverzoegerung_runter)

'   Lamellenkupplung fest anziehen, falls durchrutschend

    (L.L.antrieb_getr_aktugang) 1 >= (L.L.cockpit_throttle) 0.5 > && (L.L.velocity) 5 > && (L.L.schalttimer) 2 > &&
        (L.L.schalttimer) 2.1 < && (L.L.antrieb_getr_fest) ! &&
    {if}
        (L.L.engine_n) (F.L.engine_M_maxThrottle) 200 + (L.L.antrieb_M_limit_h) 1 max *
            (S.L.antrieb_schaltmoment_soll) (S.L.antrieb_schaltmoment)

        (L.L.antrieb_getr_downshift_forced) 1 =
        {if}
            2 (S.L.antrieb_getr_downshift_forced)
        {endif}
    {endif}

'   Timer fuer Soundfading

    (L.L.antrieb_getr_aktugang) 1 =
    {if}
        (L.L.sound_fade_g1) (L.S.timegap) + 1 min (S.L.sound_fade_g1)
    {else}
        (L.L.sound_fade_g1) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g1)
    {endif}
    (L.L.antrieb_getr_aktugang) 2 =
    {if}
        (L.L.sound_fade_g2) (L.S.timegap) + 1 min (S.L.sound_fade_g2)
    {else}
        (L.L.sound_fade_g2) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g2)
    {endif}
    (L.L.antrieb_getr_aktugang) 3 =
    {if}
        (L.L.sound_fade_g3) (L.S.timegap) + 1 min (S.L.sound_fade_g3)
    {else}
        (L.L.sound_fade_g3) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g3)
    {endif}
    (L.L.antrieb_getr_aktugang) 4 =
    {if}
        (L.L.sound_fade_g4) (L.S.timegap) + 1 min (S.L.sound_fade_g4)
    {else}
        (L.L.sound_fade_g4) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g4)
    {endif}
    (L.L.antrieb_getr_aktugang) 5 =
    {if}
        (L.L.sound_fade_g5) (L.S.timegap) + 1 min (S.L.sound_fade_g5)
    {else}
        (L.L.sound_fade_g5) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g5)
    {endif}
    (L.L.antrieb_getr_aktugang) 6 =
    {if}
        (L.L.sound_fade_g6) (L.S.timegap) + 1 min (S.L.sound_fade_g6)
    {else}
        (L.L.sound_fade_g6) (L.S.timegap) 4 * - 0 max (S.L.sound_fade_g6)
    {endif}

'   Drehmomentsperre

    (C.L.antrieb_engine_drehmomentsperre) 1 = (L.L.antrieb_getr_aktugang) 0 > && (L.L.schalttimer) 0 > && (C.L.antrieb_wandler_locked) &&
        (L.L.antrieb_wandler_lockup) 1 = &&
    {if}
        (L.L.antrieb_M_limit_h)
    {else}
        1
    {endif}
    (S.L.engine_M_restriction)

'   Wandlerüberbrückung

    (L.L.antrieb_getr_aktugang) s0

    (C.L.antrieb_wandler_locked)
    {if}
        (L.L.antrieb_wandler_lockup) !
        {if}
            l0 0 >
            {if}
                -1 s1 s2

                (L.L.schalttimer) 0.5 >
                {if}
                    l0 3 <
                    {if}
                        (L.L.cockpit_throttle) (C.L.antrieb_wandler_lockup) > (L.L.antrieb_beschleunigung) 0 > ||
                        {if}
                            (C.L.antrieb_wandler_lockmode) 0 = (C.L.antrieb_wandler_lockmode) 2 = || (C.L.antrieb_wandler_lockmode) 1 = l0 2 = && ||
                            {if}
                                l0 1 =
                                {if}
                                    (L.L.antrieb_kickdown)
                                    {if}
                                        (C.L.antrieb_wandler_lockup_spd1_kd)
                                    {else}
                                        (C.L.antrieb_wandler_lockup_spd1) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) *
                                    {endif}
                                {else}
                                    (L.L.antrieb_kickdown)
                                    {if}
                                        (C.L.antrieb_wandler_lockup_spd2_kd)
                                    {else}
                                        (C.L.antrieb_wandler_lockup_spd2) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) *
                                    {endif}
                                {endif}
                                s2
                            {endif}
                        {endif}
                    {else}
                        1 s1
                    {endif}
                {endif}

                l1 1 = l2 -1 = ! l2 (L.L.antrieb_n_kardanwelle) < && ||
                {if}
                    (L.L.cockpit_throttle) 0.6 max (S.L.antrieb_M_restriction)
                    1 (S.L.antrieb_wandler_lockup)
                    (M.L.antrieb_getr_schaltmoment) (S.L.antrieb_schaltmoment_soll) (S.L.antrieb_schaltmoment)
                    0 (S.L.schaltverzoegerung) (S.L.schalttimer)
                {endif}
            {endif}
        {else}

            l0 0 >
            {if}
                -1 s2

                l0 2 <
                {if}
                    (C.L.antrieb_wandler_lockmode) 1 = (L.L.cockpit_throttle) (C.L.antrieb_wandler_lockup) <= (L.L.antrieb_beschleunigung) 0 <= && ||
                    {if}
                        l0 1 =
                        {if}
                            (C.L.antrieb_wandler_unlock_spd1) s2
                        {endif}
                    {endif}
                {endif}

                l2 -1 = ! l2 (L.L.antrieb_n_kardanwelle) > &&
                {if}
                    0 (S.L.antrieb_wandler_lockup)
                {endif}
            {endif}
        {endif}
    {endif}

'   Schaltpunktdynamik - Getriebe reagiert damit nun unterschiedlich auf die Pedalstellung

    (C.L.topodyn_enabled) 1 =
    {if}
        (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 1 = && (L.L.antrieb_beschleunigung) 0.8 <= &&
            (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 2 = && (L.L.antrieb_beschleunigung) 0.6 <= && ||
            (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 3 = && (L.L.antrieb_beschleunigung) 0.4 <= && ||
            (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 4 = && (L.L.antrieb_beschleunigung) 0.3 <= && ||
            (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 5 = && (L.L.antrieb_beschleunigung) 0.2 <= && ||
            (L.L.cockpit_throttle) 0.85 >= (L.L.antrieb_getr_aktugang) 6 = && (L.L.antrieb_beschleunigung) 0.1 <= && ||
            (L.L.velocity) (C.L.topodyn_on_v_max) < && (L.L.antrieb_M_restriction) 0.85 >= &&
        {if}
            (L.L.torque_inc) (L.S.Timegap) 10 / + 0.1 min (S.L.torque_inc)
            (L.L.topodyn_intervene_Timer) (L.S.Timegap) 2 * + 1 min (S.L.topodyn_intervene_Timer) 1 =
            {if}
                1 (S.L.topodyn_active)
                1.1 (S.L.topodyn_factor)
            {endif}
        {else}
            (L.L.topodyn_intervene_Timer) (L.S.Timegap) 2 * - 0 max (S.L.topodyn_intervene_Timer)
            (L.L.torque_inc) (L.S.Timegap) 10 / - 0 max (S.L.torque_inc)
        {endif}
    {else}
        1 (S.L.topodyn_factor)
    {endif}

    (C.L.topodyn_enabled) 1 =
    {if}
        (L.L.antrieb_beschleunigung) 1.2 > (L.L.antrieb_getr_aktugang) 1 = &&
            (L.L.antrieb_beschleunigung) 1.0 > (L.L.antrieb_getr_aktugang) 2 = && ||
            (L.L.antrieb_beschleunigung) 0.8 > (L.L.antrieb_getr_aktugang) 3 = && ||
            (L.L.antrieb_beschleunigung) 0.6 > (L.L.antrieb_getr_aktugang) 4 = && ||
            (L.L.antrieb_beschleunigung) 0.4 > (L.L.antrieb_getr_aktugang) 5 = && ||
            (L.L.antrieb_beschleunigung) 0.2 > (L.L.antrieb_getr_aktugang) 6 = && ||
            (L.L.velocity) (C.L.topodyn_off_v_min) > || (L.L.cockpit_throttle) 0 = ||
        {if}
            (L.L.topodyn_idle_Timer) (L.S.Timegap) 1 * + 1 min (S.L.topodyn_idle_Timer) 1 =
            {if}
                0 (S.L.topodyn_active)
                1 (S.L.topodyn_factor)
            {endif}
        {else}
            (L.L.topodyn_idle_Timer) (L.S.Timegap) 2 * - 0 max (S.L.topodyn_idle_Timer)
        {endif}
    {endif}

    (L.L.cockpit_throttle) 0.90 > (C.L.Getriebemodus) 2 = (L.L.cockpit_throttle) 0.1 > && ||
        (C.L.gearbox_version) 1 = (L.L.cockpit_brake) 0 > && || (L.L.topodyn_active) ||
    {if}
        (L.L.throttle_internb) (L.S.Timegap) 1 * + 1 min (S.L.throttle_internb) 1 =
        {if}
            1.3 (L.L.topodyn_factor) * (S.L.shift_dynamics_high)
            (C.L.gearbox_version) 1 = (L.L.cockpit_brake) 0 > &&
            {if}
                (L.L.antrieb_getr_aktugang) 3 = ! (L.L.antrieb_getr_aktugang) 2 = ! &&
                {if}
                    1.5 (S.L.shift_dynamics_high)
                {else}
                    1.2 (S.L.shift_dynamics_high)
                {endif}
                1 (S.L.schaltsperre)
            {endif}
        {endif}
    {else}
        (L.L.throttle_internb) (L.S.Timegap) 1 * - 0 max (S.L.throttle_internb) 0 = (L.L.schaltsperre) ||
        {if}
            1 (S.L.shift_dynamics_high)
            0 (S.L.schaltsperre)
        {endif}
    {endif}

    (L.L.cockpit_throttle) 0.1 > (L.L.cockpit_throttle) 0.80 < && (C.L.Getriebemodus) 1 =
        (L.L.cockpit_throttle) 0.1 > && ||
    {if}
        (L.L.throttle_intern) (L.S.Timegap) 1 * + 1 min (S.L.throttle_intern) 1 =
        {if}
            0.9 (S.L.shift_dynamics_low)
        {endif}
    {else}
        (L.L.throttle_intern) (L.S.Timegap) 1 * - 0 max (S.L.throttle_intern) 0 =
        {if}
            1 (S.L.shift_dynamics_low)
        {endif}
    {endif}

    (L.L.antrieb_getr_gangwahl) (L.L.antrieb_getr_aktugang) &&
    {if}
'       Bestimmung des Sollwertes für das Wandlermoment

        (L.L.n_Wheel) (L.L.antrieb_getr_ratio) * (S.L.antrieb_getr_n_ab) (L.L.engine_n) / 1 min (S.L.antrieb_wandler_sollwert)

'       Glättung des Wandlermomentes

        (L.L.antrieb_wandler_ny) (L.L.antrieb_wandler_sollwert) >
        {if}
            (L.L.antrieb_wandler_ny) (L.S.Timegap) 0.5 * - (L.L.antrieb_wandler_sollwert) max (S.L.antrieb_wandler_ny)
        {else}
            (L.L.antrieb_wandler_ny) (L.S.Timegap) 0.5 * + (L.L.antrieb_wandler_sollwert) min (S.L.antrieb_wandler_ny)
        {endif}
    {endif}

'   Fallunterscheidung, ob Getriebe reibt oder wandelt oder nicht:

    (L.L.antrieb_getr_fest)
    {if}

'#################################################################################
'       Berechnung der Beschleunigung des Antriebsstrangs (am Motorflansch)
'#################################################################################

        (L.L.n_Wheel) (L.L.antrieb_getr_ratio) * (L.L.engine_n) - 2 * pi * 60 / (L.S.Timegap) /

'       Berechnung des Momentes am Motor durch Trägheit:

        (C.L.engine_J) * s0

'       Berechnung des vom Antriebsstrang durchgeleiteten Momentes

        l0 /-/ (L.L.engine_M) (L.L.engine_M_additional_load) - + s1 0 >
        {if}
            l1 (L.L.antrieb_M_limit_l_g_fest) * s1
        {endif}

'       Wenn das Antriebsmoment zu groß ist oder ein Wandlergang eingelegt wird, rutscht die Kupplung durch:

        l1 abs (L.L.antrieb_schaltmoment) > (L.L.antrieb_getr_gangwahl) 1 <= || (L.L.antrieb_wandler_lockup) ! || (C.L.antrieb_wandler_locked) ! ||
        {if}
            0 (S.L.antrieb_getr_fest)
        {else}

'           Setzen des Momentes

            l1 (S.L.antrieb_getr_M_an)

'           Berechnung des Getriebeabtriebmomentes:

            (L.L.antrieb_getr_M_an) (L.L.antrieb_getr_ratio) * (C.L.gear_efficiency) * (S.L.M_Wheel)

'           Drehzahl des Motors ergibt sich aus der Getriebeabtriebswelle:

            (L.L.antrieb_getr_ratio) (L.L.n_Wheel) * (S.L.engine_n)

            0 (S.L.antrieb_getr_downshift_forced)
        {endif}

        0 (S.L.antrieb_wandler_moment)

    {else}

'#################################################################################
'       Bestimmen, ob Leerlauf ist...:
'#################################################################################

        (L.L.antrieb_getr_gangwahl) 1 = (L.L.antrieb_getr_aktugang) 0 = ||
        {if}

'           ... Antriebs- und Getriebeantriebsmoment sind 0:
            0 (S.L.M_Wheel) (S.L.antrieb_getr_M_an)

            (M.L.engine_acceleration)
            0 (S.L.antrieb_wandler_moment) (S.L.antrieb_wandler_ny) (S.L.antrieb_wandler_sollwert)
                (S.L.antrieb_getr_downshift_forced)

'           Wenn Neutralgang beim Bremsen eingelegt wird
            (L.L.antrieb_getr_gangwahl) 1 =
            {if}
                0 (S.L.antrieb_retarder_wasrunning) (S.L.antrieb_gear_engaged_timer) (S.L.nbs_fade)
            {endif}

        {else}

'#################################################################################
'           Bestimmen, ob ein Wandlergang eingelegt ist...:
'#################################################################################

            (L.L.antrieb_wandler_lockup) ! (C.L.antrieb_wandler_locked) ! ||
            {if}

                (L.L.antrieb_wandler_moment) abs (L.L.antrieb_getr_M_an) abs >
                {if}
                    (L.L.antrieb_wandler_moment) 0 >
                    {if}
                        (L.L.antrieb_getr_M_an) (C.L.antrieb_wandler_fillrate) (L.S.Timegap) * + (S.L.antrieb_getr_M_an)
                        (L.L.antrieb_wandler_moment) (L.L.antrieb_getr_M_an) <
                        {if}
                            (L.L.antrieb_wandler_moment) (L.L.antrieb_n_kardanwelle) (F.L.antrieb_wandler_fluidrotation)
                                (L.L.cockpit_throttle) (F.L.antrieb_wandler_throttle) * - (S.L.antrieb_getr_M_an)
                        {endif}
                    {else}
                        (L.L.antrieb_getr_M_an) (C.L.antrieb_wandler_fillrate) (L.S.Timegap) * - (S.L.antrieb_getr_M_an)
                        (L.L.antrieb_wandler_moment) (L.L.antrieb_getr_M_an) >
                        {if}
                            (L.L.antrieb_wandler_moment) (L.L.antrieb_n_kardanwelle) (F.L.antrieb_wandler_fluidrotation)
                                (L.L.cockpit_throttle) (F.L.antrieb_wandler_throttle) * - (S.L.antrieb_getr_M_an)
                        {endif}
                    {endif}
                {else}
                    (L.L.antrieb_wandler_moment) (L.L.antrieb_n_kardanwelle) (F.L.antrieb_wandler_fluidrotation)
                        (L.L.cockpit_throttle) (F.L.antrieb_wandler_throttle) * - (S.L.antrieb_getr_M_an)
                {endif}

'#################################################################################
'               Wandler Rückwärtsgang:
'#################################################################################

                (L.L.antrieb_getr_gangwahl) 0 =
                {if}
                    (L.L.antrieb_n_kardanwelle) 2 * (S.L.antrieb_getr_n_ab) /-/ (L.L.engine_n) / (S.L.antrieb_wandler_ny)

'                    Berechnung des Motor-Antriebsmomentes

                        (L.L.engine_n) 2 * 3.14 * 60 / (L.L.engine_n) 2 * 3.14 * 60 / * s1
                        0.0042 (L.L.antrieb_wandler_ny) (F.L.antrieb_wandler_lambda) * l1 * (S.L.antrieb_wandler_moment)

'                    Berechnung des Rad-Abtriebsmomentes

                        (L.L.antrieb_wandler_ny) (F.L.antrieb_wandler_my) (L.L.antrieb_getr_M_an) * (C.L.antrieb_i_achse) * 0.9 * /-/ (C.L.gear_efficiency) *
                            (L.L.antrieb_n_kardanwelle) (F.L.antrieb_reverse_torque_reduction) * (S.L.M_Wheel)

'#################################################################################
'               Wandler Vorwärtsgang:
'#################################################################################

                {else}
'                   Berechnung des Motor-Antriebsmomentes

                    (L.L.engine_n) 2 * 3.14 * 60 / (L.L.engine_n) 2 * 3.14 * 60 / * s1
                    0.0042 (L.L.antrieb_wandler_ny) (F.L.antrieb_wandler_lambda) * l1 * (L.L.engine_n) (F.L.antrieb_wandler_lowrpmsoftness) * s1 0 >
                    {if}
                        l1 (L.L.antrieb_M_limit_l_wandler) * s1
                    {endif}
                    l1 (S.L.antrieb_wandler_moment)

'                   Berechnung des Rad-Abtriebsmomentes

                    (L.L.engine_M) (C.L.antrieb_i_achse) * (C.L.antrieb_wandler_factor) * (C.L.gear_efficiency) * (L.L.velocity) (F.L.antrieb_wandler_wirkungsgrad) *
                        (L.L.engine_n) (F.L.antrieb_wandler_idleforce) (L.L.velocity) (F.L.antrieb_wandler_idle_fadeout) * max (S.L.M_Wheel)
                {endif}

'               Vergleich der Ein- und Ausgangsleistungen:
                (L.L.M_Wheel) (L.L.n_Wheel) * 3.14 * 30000 / (S.L.antrieb_gesamtleistung)
                (L.L.antrieb_gesamtleistung) (L.L.antrieb_motorleistung) / (S.L.antrieb_eta)

                (M.L.engine_acceleration)

                0 (S.L.antrieb_getr_downshift_forced)

'#################################################################################
'           Sonst ist ein normaler Gang eingelegt:
'#################################################################################

            {else}

'               Berechnung der Drehzahldifferenz:

                (L.L.antrieb_getr_ratio) (L.L.n_Wheel) * (L.L.engine_n) - s0

'               An dieser Stelle muss ggf. noch das unterschiedliche Kupplungsmoment eingesetzt werden:

                l0 0 <
                {if}
                    (L.L.antrieb_schaltmoment)
                {else}
                    (L.L.antrieb_schaltmoment) /-/
                {endif}
                s1 0 >
                {if}
                    l1 (L.L.antrieb_M_limit_l_g) * s1
                {endif}
                l1 (S.L.antrieb_getr_M_an) (L.L.antrieb_getr_ratio) * (C.L.gear_efficiency) * (S.L.M_Wheel)

                (M.L.engine_acceleration)

'               Prüfen, ob sich das Vorzeichen der Drehzahldifferenz umgekehrt hat:

                (L.L.antrieb_getr_ratio) (L.L.n_Wheel) * (L.L.engine_n) - l0 * 0 <= (L.L.antrieb_getr_downshift_forced) 2 = || (L.L.engine_n) 560 > &&
                {if}
                    (L.L.antrieb_getr_ratio) (L.L.n_Wheel) * (S.L.engine_n)
                    (L.L.n_Wheel) (L.L.antrieb_getr_ratio) * (L.L.engine_n) - 2 * pi * 60 / (L.S.Timegap) /

'                   Berechnung des Momentes am Motor durch Trägheit:

                    (C.L.engine_J) * s0

'                   Berechnung des vom Antriebsstrang durchgeleiteten Momentes

                    l0 /-/ (L.L.engine_M) (L.L.engine_M_additional_load) - + (S.L.antrieb_getr_M_an) s1

'                   Berechnung des Getriebeabtriebmomentes:

                    l1 abs (L.L.antrieb_schaltmoment) abs <
                    {if}
                        l1 0 >
                        {if}
                            l1 (L.L.antrieb_M_limit_l_g_fest) * s1
                        {endif}
                        l1 (S.L.antrieb_getr_M_an) (L.L.antrieb_getr_ratio) * (C.L.gear_efficiency) * (S.L.M_Wheel)
                        1 (S.L.antrieb_getr_fest)
                        (M.L.antrieb_getr_schaltmoment_runter) (S.L.antrieb_schaltmoment_soll) (S.L.antrieb_schaltmoment)
                    {else}
                        (L.L.antrieb_schaltmoment) /-/ s1 0 >
                        {if}
                            l1 (L.L.antrieb_M_limit_l_g) * s1
                        {endif}
                        l1 (S.L.antrieb_getr_M_an) (L.L.antrieb_getr_ratio) * (C.L.gear_efficiency) * (S.L.M_Wheel)
                    {endif}
                {endif}

                0 (S.L.antrieb_wandler_moment)

            {endif}
        {endif}
    {endif}


'#################################################################################
'   Retarder
'#################################################################################
    (L.L.cockpit_brake) 0.05 > (L.L.antrieb_retarder_sw_direkt) || (L.L.cockpit_retarderhebel) 0 > ||
        (L.L.antrieb_retarder_sw) ! && (L.L.antrieb_getr_gangwahl) 1 > && (L.L.cockpit_throttle) 0.1 < &&
        (L.L.velocity) 0.5 > && (L.L.antrieb_failure_general) ! && (L.L.bremse_ABS_eingriff) ! &&
        (L.L.schaltverzoegerung_runter) 0.1 > && (L.L.vdv_error_retarder_active) ! &&
    {if}
        0 (S.L.throttle_interne)
        (L.L.antrieb_retarder_time_elapsed) (L.S.Timegap) + (S.L.antrieb_retarder_time_elapsed)

        (L.L.antrieb_retarder_time_elapsed) (C.L.antrieb_retarder_startdelay) >
        {if}
            1 (S.L.antrieb_retarder)
        {endif}

        (L.L.cockpit_retarderhebel) 0 =
        {if}
            (L.L.cockpit_brake) 0.1 <
            {if}
                1 (S.L.antrieb_retarderstufe)
            {else}
                2 (S.L.antrieb_retarderstufe)
            {endif}
        {endif}

'   Ausschalten des Retarders:

    {else}
        (L.L.antrieb_retarder) (L.L.cockpit_brake) 0.05 > && (L.L.schaltverzoegerung_runter) 0.1 < && (L.L.velocity) 0.5 > &&
        {if}
            (T.L.ev_retarder_hiss)
        {endif}

        (L.L.velocity) 0.5 <= (L.L.throttle_interne) ! &&
        {if}
            (T.L.ev_retarder_disengage)
            1 (S.L.throttle_interne)
        {endif}

        (L.L.cockpit_brake) 0.05 <= (L.L.velocity) 0.5 > && (L.L.antrieb_retarder) &&
        {if}
            (T.L.ev_retarder_disengage)
        {endif}

        0 (S.L.antrieb_retarder) (S.L.antrieb_retarder_time_elapsed)
    {endif}

'   Bremsmoment des Retarders:

    (L.L.antrieb_retarder) 1 =
    {if}
        (L.L.antrieb_retarderstufe) 1 =
        {if}
            (L.L.antrieb_n_kardanwelle) 29.3 / (F.L.retarder_stufe1) 0.7 * (C.L.antrieb_i_achse) /-/ * (L.L.antrieb_retarder_time_elapsed) (F.L.retarder_fadein) *
                (S.L.antrieb_retardermoment) (L.L.M_Wheel) + (S.L.M_Wheel)
        {else}
            (L.L.antrieb_retarderstufe) 2 =
            {if}
                (L.L.antrieb_retardermoment) (L.S.Timegap) 10000 * - s1
                (L.L.antrieb_n_kardanwelle) 29.3 / (F.L.retarder_stufe2) 0.7 * (C.L.antrieb_i_achse) /-/ * (L.L.antrieb_retarder_time_elapsed) (F.L.retarder_fadein) * s2
                l1 l2 max (S.L.antrieb_retardermoment) (L.L.M_Wheel) + (S.L.M_Wheel)
            {else}
                (L.L.antrieb_retarderstufe) 3 =
                {if}
                    (L.L.antrieb_retardermoment) (L.S.Timegap) 10000 * - s1
                    (L.L.antrieb_n_kardanwelle) 29.3 / (F.L.retarder_stufe3) 0.7 * (C.L.antrieb_i_achse) /-/ * (L.L.antrieb_retarder_time_elapsed) (F.L.retarder_fadein) * s2
                    l1 l2 max (S.L.antrieb_retardermoment) (L.L.M_Wheel) + (S.L.M_Wheel)
                {endif}
            {endif}
        {endif}
    {else}
'       Ausfaden der Retarder-Bremsstärke
        (L.L.antrieb_retardermoment) (L.S.Timegap) 1000 * + 0 min (S.L.antrieb_retardermoment) (L.L.M_Wheel) + (S.L.M_Wheel)
        (L.L.antrieb_retarder_volume) (L.S.Timegap) - 0 max (S.L.antrieb_retarder_volume) s0
        l0 0 =
        {if}
            0 (S.L.antrieb_wendesatz)
        {endif}

    {endif}

'#################################################################################
'   Automatische Neutralschaltung bei Stillstand
'#################################################################################

    (L.L.antrieb_gear_engaged_timer) (L.S.Timegap) + (S.L.antrieb_gear_engaged_timer)

    (L.L.antrieb_n_kardanwelle) (C.L.antrieb_neutral_maxspeed) < (L.L.antrieb_getr_gangwahl) 1 > && (L.L.antrieb_retarder) ! &&
    {if}
        (L.L.bremse_p_Brzyl_HA) (C.L.antrieb_neutral_brakepressure) >
        (L.L.bremse_feststell) ||
        {if}
            (L.L.antrieb_neutral_requested) !
            {if}
                1 (S.L.antrieb_neutral_requested)
                0 (S.L.antrieb_gear_engaged_timer) (S.L.antrieb_wandler_moment) (S.L.antrieb_wandler_ny) (S.L.antrieb_wandler_sollwert)
            {endif}

            (L.L.antrieb_neutral_requested) (L.L.antrieb_gear_engaged_timer) (C.L.antrieb_gear_engaged_mintime) >= &&
            {if}
                0 (S.L.antrieb_getr_aktugang) (S.L.antrieb_schaltmoment_soll) (S.L.antrieb_schaltmoment)
                (L.L.nbs_fade) (L.S.timegap) + (S.L.nbs_fade)
            {endif}
        {else}

            1 (S.L.antrieb_getr_aktugang)
            0 (S.L.antrieb_neutral_requested) (S.L.antrieb_gear_engaged_timer)
            0 (S.L.nbs_fade)
        {endif}
    {endif}

'#################################################################################
'   Planetengetriebe-Wendesatz (Fjü-Sound)
'#################################################################################

    (L.L.antrieb_retarder)
    (L.L.antrieb_getr_gangwahl) 0 = ||
    {if}
        1 (S.L.antrieb_wendesatz)
        (L.L.antrieb_retarder_volume) (L.S.Timegap) + 1 min (S.L.antrieb_retarder_volume)
'       Rücksetzen auf 0 erfolgt beim Fadeout des Retarderbremsmoments
    {endif}
{end}

{macro:antrieb_cfg_apply_overrides}
    (C.L.global_cfg_no_cti_overrides) 1 = ! s0

    (L.L.antrieb_getr_cfg_on_demand_standstill_non_neutral_transmission_mode_update_requires_braking) s1 0 = l1 1 = || !
    {if}
        (L.L.antrieb_getr_cfg_cti_standstill_non_neutral_transmission_mode_update_requires_braking) s1 0 = l1 1 = || l0 && (C.L.antrieb_getr_cfg_no_cti_override_standstill_non_neutral_transmission_mode_update_requires_braking) 1 = ! && !
        {if}
            -1 s1
        {endif}
    {endif}
    l1 -1 = !
    {if}
        l1 (S.L.antrieb_getr_cfg_effective_standstill_non_neutral_transmission_mode_update_requires_braking)
    {endif}
{end}

'#################################################################################
' Bestimmung der Schaltgeschwindigkeiten
'#################################################################################

{macro:antrieb_getr_chkNxtGear}

'   Verzögerung der Kickdown-Schaltung:

    (C.L.kickdown_enabled) 1 =
    {if}
        (L.L.cockpit_throttle) 1 =
        {if}
            (L.L.antrieb_kickdown_stellglied) (L.S.Timegap) 2 * + 1 min (S.L.antrieb_kickdown_stellglied)
            1 =
            {if}
                1 (S.L.antrieb_kickdown)
            {endif}
        {else}
            (L.L.antrieb_kickdown_stellglied) (L.S.Timegap) 1 * - 0 max (S.L.antrieb_kickdown_stellglied)
            0 =
            {if}
                0 (S.L.antrieb_kickdown)
            {endif}
        {endif}
    {endif}

'   Verzögerung der gaspedalabhängigen Schaltschwelle:

    (L.L.cockpit_throttle) (L.L.antrieb_throttle_stellglied) >
    {if}
        (L.L.antrieb_throttle_stellglied) (L.S.Timegap) 1 * + (L.L.cockpit_throttle) min (S.L.antrieb_throttle_stellglied)
    {else}
        (L.L.antrieb_throttle_stellglied) (L.S.Timegap) 0.5 * - (L.L.cockpit_throttle) max (S.L.antrieb_throttle_stellglied)
    {endif}

'   Berechnen der aktuellen Beschleunigung:

    (L.L.Velocity) (L.L.antrieb_last_geschwindigkeit) - 3.6 / (L.S.Timegap) / (S.L.antrieb_beschleunigung) s7
    (L.L.Velocity) (S.L.antrieb_last_geschwindigkeit)

    (L.L.antrieb_getr_aktugang) s1
    (L.L.antrieb_n_kardanwelle) s5
    (L.L.antrieb_getr_gangwahl) s6

    l1 0 =
    {if}

'       Neutralgang

    {else}

        l1 1 =
        {if}

'           1. Gang

            (L.L.antrieb_kickdown) (C.L.Getriebemodus) 2 = ||
            {if}
                (C.L.antrieb_getr_autoSwUpkickdnSpd1) s3 s4
            {else}
                (C.L.antrieb_wandler_lockmode) 1 = (L.L.cockpit_throttle) (C.L.antrieb_wandler_lockup) < ||
                {if}
                    (C.L.antrieb_getr_autoSwUpMaxSpd1c) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                {else}
                    (C.L.antrieb_getr_autoSwUpMaxSpd1) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                {endif}
            {endif}

            l6 2 = (C.L.antrieb_getr_constrained_upshifting) &&
            {if}
                (C.L.antrieb_getr_autoSwUpMaxSpd1f) l5 <
                {if}
                    1
                {else}
                    -1
                {endif}
            {else}
                0
            {endif}
            (S.L.r10) (M.L.antrieb_getr_upshift_constraint)

            (M.L.antrieb_getr_chkCh) l5 < l6 2 > && (L.L.r10) 2 = || (L.L.schalttimer) 1 > &&
            {if}
                2 (S.L.antrieb_getr_aktugang)

                (C.L.antrieb_wandler_lockmode) 0 = (C.L.antrieb_wandler_lockmode) 1 = || (L.L.antrieb_kickdown) ||
                    (L.L.cockpit_throttle) (C.L.antrieb_wandler_lockup) < (L.L.antrieb_wandler_lockup) ! && ||
                    (C.L.gearbox_version) 1 = ! && (L.L.r10) ! &&
                {if}
                    0 (S.L.antrieb_wandler_lockup)
                    (L.L.antrieb_kickdown)
                    {if}
                        (T.L.ev_gear_upshift2)
                    {else}
                        (T.L.ev_gear_upshift1)
                    {endif}
                {else}
                    (T.L.ev_gear_upshift2)
                {endif}
            {else}

                (C.L.antrieb_wandler_unlock_spd1) l5 > (L.L.schalttimer) 0.5 > (L.L.engine_n) (C.L.engine_governed_idle_RPM) 300 + < &&
                    (L.L.cockpit_throttle) 0.85 >= && || (L.L.antrieb_wandler_lockup) &&
                {if}
                    (M.L.antrieb_getr_schaltmoment_runter) (S.L.antrieb_schaltmoment_soll)
                    0 (S.L.antrieb_wandler_lockup) (S.L.antrieb_schaltmoment) (S.L.schalttimer) (S.L.schaltverzoegerung_runter)
                {endif}
            {endif}

        {else}

            l1 2 =
            {if}

'               2. Gang

                (L.L.antrieb_kickdown)
                {if}
                    (C.L.antrieb_getr_autoSwUpkickdnSpd2) s3 s4
                {else}
                    (C.L.antrieb_getr_autoSwUpMaxSpd2) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                {endif}

                l6 1 > l6 4 < && (C.L.antrieb_getr_constrained_upshifting) &&
                {if}
                    (C.L.antrieb_getr_autoSwUpMaxSpd2f) l5 <
                    {if}
                        1
                    {else}
                        -1
                    {endif}
                {else}
                    0
                {endif}
                (S.L.r10) (M.L.antrieb_getr_upshift_constraint)

                (M.L.antrieb_getr_chkCh) l5 < l6 3 > && (L.L.r10) 2 = || (L.L.schalttimer) 1 > &&
                {if}
                    3 (S.L.antrieb_getr_aktugang)
                    (T.L.ev_gear_upshift3)
                {else}

                    l6 2 = (L.L.antrieb_kickdown) && (C.L.antrieb_getr_constrained_downshifting) 2 = &&
                        (C.L.antrieb_getr_autoSwDnMaxSpd2f) l5 > && (S.L.r10)

                    (L.L.engine_n) (C.L.engine_governed_idle_RPM) 300 + < (L.L.cockpit_throttle) 0.85 >= && (L.L.r10) ||
                        (L.L.schalttimer) 0.5 > && (L.L.engine_n) (C.L.engine_governed_idle_RPM) < || (L.L.antrieb_wandler_lockup) &&
                        (L.L.antrieb_wandler_lockup) ! (L.L.velocity) 2 < && (S.L.r11) || (L.L.schaltverzoegerung) 1 > &&
                        (L.L.schaltverzoegerung_runter) 1 > &&
                    {if}
                        (L.L.r10) (L.L.r11) ||
                        {if}
                            1 (S.L.antrieb_getr_aktugang)
                            (T.L.ev_gear_downshift3)
                        {endif}
                        (L.L.r10) !
                        {if}
                            0 (S.L.antrieb_wandler_lockup)
                        {endif}
                    {endif}
                {endif}

            {else}

                l1 3 =
                {if}

'                   3. Gang

                    (L.L.antrieb_kickdown)
                    {if}
                        (C.L.antrieb_getr_autoSwUpkickdnSpd3) s3 s4
                    {else}
                        (C.L.antrieb_getr_autoSwUpMaxSpd3) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                    {endif}

                    l6 1 > l6 5 < && (C.L.antrieb_getr_constrained_upshifting) &&
                    {if}
                        (C.L.antrieb_getr_autoSwUpMaxSpd3f) l5 <
                        {if}
                            1
                        {else}
                            -1
                        {endif}
                    {else}
                        0
                    {endif}
                    (S.L.r10) (M.L.antrieb_getr_upshift_constraint)

                    (M.L.antrieb_getr_chkCh) l5 < l6 5 = && (L.L.r10) 2 = || (L.L.schalttimer) 1 > &&
                    {if}
                        4 (S.L.antrieb_getr_aktugang)
                        (T.L.ev_gear_upshift4)
                    {else}

                        (L.L.antrieb_kickdown)
                        {if}
                            (C.L.antrieb_getr_autoSwDnkickdnSpd3) s3 s4
                        {else}
                            (C.L.antrieb_getr_autoSwDnMinSpd3) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s4
                            (C.L.antrieb_getr_autoSwDnMaxSpd3) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3
                        {endif}

                        l6 1 > l6 4 < && (L.L.antrieb_kickdown) && (C.L.antrieb_getr_constrained_downshifting) 2 = &&
                            (C.L.antrieb_getr_autoSwDnMaxSpd3f) l5 > && (S.L.r10)

                        (M.L.antrieb_getr_chkCh) l5 > (L.L.antrieb_retarder_armed) ! && (L.L.r10) ||
                            (C.L.antrieb_getr_autoSwDnMinSpd3) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * l5 > ||
                            (L.L.schaltverzoegerung) 1 > && (L.L.schaltverzoegerung_runter) 1 > &&
                            (L.L.engine_n) (C.L.engine_governed_idle_RPM) < ||
                        {if}
                            2 (S.L.antrieb_getr_aktugang)
                            (T.L.ev_gear_downshift4)
                            (C.L.antrieb_wandler_locked) (C.L.gearbox_version) 1 = (L.L.cockpit_throttle) 0 = && (L.L.r10) || &&
                                (S.L.antrieb_wandler_lockup)
                        {endif}
                    {endif}

                {else}

                    l1 4 =
                    {if}

'                       4. Gang

                        (L.L.antrieb_kickdown)
                        {if}
                            (C.L.antrieb_getr_autoSwUpkickdnSpd4) s3 s4
                        {else}
                            (C.L.antrieb_getr_autoSwUpMaxSpd4) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                        {endif}

                        l6 1 > l6 5 < && (C.L.antrieb_getr_constrained_upshifting) 2 = &&
                        {if}
                            (C.L.antrieb_getr_autoSwUpMaxSpd4f) l5 <
                            {if}
                                1
                            {else}
                                -1
                            {endif}
                        {else}
                            0
                        {endif}
                        (S.L.r10) (M.L.antrieb_getr_upshift_constraint)

                        (M.L.antrieb_getr_chkCh) l5 < l6 5 = && (L.L.r10) 2 = || (L.L.schalttimer) 1 > &&
                        {if}
                            5 (S.L.antrieb_getr_aktugang)
                            (T.L.ev_gear_upshift5)
                        {else}

                            (L.L.antrieb_kickdown)
                            {if}
                                (C.L.antrieb_getr_autoSwDnkickdnSpd4) s3 s4
                            {else}
                                (C.L.antrieb_getr_autoSwDnMinSpd4) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s4
                                (C.L.antrieb_getr_autoSwDnMaxSpd4) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3
                            {endif}

                            l6 1 > l6 5 < && (L.L.antrieb_kickdown) && (C.L.antrieb_getr_constrained_downshifting) 2 = &&
                                (C.L.antrieb_getr_autoSwDnMaxSpd4f) l5 > && (S.L.r10)

                            (M.L.antrieb_getr_chkCh) l5 > (L.L.antrieb_retarder_armed) ! && (L.L.r10) ||
                                (C.L.antrieb_getr_autoSwDnMinSpd4) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * l5 > ||
                                (L.L.schaltverzoegerung) 1 > && (L.L.schaltverzoegerung_runter) 1 > &&
                                (L.L.engine_n) (C.L.engine_governed_idle_RPM) < ||
                            {if}
                                3 (S.L.antrieb_getr_aktugang)
                                (T.L.ev_gear_downshift5)
                            {endif}
                        {endif}

                    {else}

                        l1 5 =
                        {if}

'                           5. Gang

                            (L.L.antrieb_kickdown)
                            {if}
                                (C.L.antrieb_getr_autoSwUpkickdnSpd5) s3 s4
                            {else}
                                (C.L.antrieb_getr_autoSwUpMaxSpd5) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3 s4
                            {endif}

                            l6 1 > l6 5 < && (C.L.antrieb_getr_constrained_upshifting) 2 = &&
                            {if}
                                (C.L.antrieb_getr_autoSwUpMaxSpd5f) l5 <
                                {if}
                                    1
                                {else}
                                    -1
                                {endif}
                            {else}
                                0
                            {endif}
                            (S.L.r10) (M.L.antrieb_getr_upshift_constraint)

                            (M.L.antrieb_getr_chkCh) l5 < l6 5 = && l7 0 > && (L.L.r10) 2 = || (L.L.schalttimer) 1 > &&
                            {if}
                                6 (S.L.antrieb_getr_aktugang)
                                (T.L.ev_gear_upshift6)
                            {else}

                                (L.L.antrieb_kickdown)
                                {if}
                                    (C.L.antrieb_getr_autoSwDnkickdnSpd5) s3 s4
                                {else}
                                    (C.L.antrieb_getr_autoSwDnMinSpd5) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s4
                                    (C.L.antrieb_getr_autoSwDnMaxSpd5) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3
                                {endif}

                                l6 1 > l6 5 < && (L.L.antrieb_kickdown) (L.L.topodyn_active) || (C.L.Getriebemodus) 2 = || &&
                                    (C.L.antrieb_getr_constrained_downshifting) && (C.L.antrieb_getr_autoSwDnMaxSpd5f) l5 > && (S.L.r10)

                                (M.L.antrieb_getr_chkCh) l5 > (L.L.antrieb_retarder_armed) ! && (L.L.r10) ||
                                    (C.L.antrieb_getr_autoSwDnMinSpd5) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * l5 > ||
                                    (L.L.schaltverzoegerung) 1 > && (L.L.schaltverzoegerung_runter) 1 > &&
                                    (L.L.engine_n) (C.L.engine_governed_idle_RPM) < ||
                                {if}
                                    4 (S.L.antrieb_getr_aktugang)
                                    (T.L.ev_gear_downshift6)
                                {endif}
                            {endif}

                        {else}

                            l1 6 =
                            {if}

'                               6. Gang

                                (L.L.antrieb_kickdown)
                                {if}
                                    (C.L.antrieb_getr_autoSwDnkickdnSpd6) s3 s4
                                {else}
                                    (C.L.antrieb_getr_autoSwDnMinSpd6) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s4
                                    (C.L.antrieb_getr_autoSwDnMaxSpd6) (L.L.shift_dynamics_high) * (L.L.shift_dynamics_low) * s3
                                {endif}

                                l6 1 > l6 5 < && (L.L.antrieb_kickdown) (L.L.topodyn_active) || (C.L.Getriebemodus) 2 = || &&
                                    (C.L.antrieb_getr_constrained_downshifting) && (C.L.antrieb_getr_autoSwDnMaxSpd6f) l5 > && (S.L.r10)

                                (M.L.antrieb_getr_chkCh) l5 > (L.L.r10) || (L.L.schaltverzoegerung) 1 > && (L.L.schaltverzoegerung_runter) 1 > &&
                                    (L.L.engine_n) (C.L.engine_governed_idle_RPM) < ||
                                {if}
                                    5 (S.L.antrieb_getr_aktugang)
                                    (T.L.ev_gear_downshift7)
                                {endif}
                            {endif}
                        {endif}
                    {endif}
                {endif}
            {endif}
        {endif}
    {endif}

'   Testweise: Differenzialdrehzahlpfeifen

    (L.L.engine_n) l5 2 * - (S.L.antrieb_n_differenz)

    (L.L.antrieb_getr_aktugang) s2 l1 - s0
    {if}
        l2 1 >
        {if}
            (L.L.cockpit_throttle) 0.6 max (S.L.antrieb_M_restriction)
        {endif}

        l0 0 >
        {if}
            (M.L.antrieb_getr_schaltmoment) (S.L.antrieb_schaltmoment_soll) (S.L.antrieb_schaltmoment)
            0 (S.L.schaltverzoegerung) (S.L.antrieb_getr_downshift_forced)
        {else}

            (M.L.antrieb_getr_schaltmoment_runter) s3
            l2 2 =
            {if}
                l3 2.5 / s3
                (L.L.antrieb_wandler_lockup) !
                {if}
                    l3 2 / s3
                {endif}
            {endif}
            l3 (S.L.antrieb_schaltmoment_soll)
            0 (S.L.antrieb_schaltmoment) (S.L.schaltverzoegerung_runter)

            (L.L.r10) (S.L.antrieb_getr_downshift_forced)

            l2 2 > (L.L.cockpit_throttle) 0 > && (L.L.r10) ! &&
            {if}
                0 (S.L.antrieb_wandler_lockup)
            {endif}
        {endif}

        0 (S.L.schalttimer)
    {endif}

    l0 l1 0 = || l1 6 = ||
    {if}
        0 (S.L.r10) (M.L.antrieb_getr_upshift_constraint)
    {endif}
{end}

{macro:antrieb_getr_chkCh}
'    Berechnung der aktuellen Hochschaltschwelle: (l3 = bei Vollgas, l4 = bei Leergas)
'    (L.L.antrieb_throttle_stellglied) s2 l3 * 1 l2 - l4 * +
    (L.L.antrieb_throttle_stellglied) 0 >
    {if}
        l3
    {else}
        l4
    {endif}
{end}

{macro:antrieb_getr_schaltmoment}
    (L.L.cockpit_throttle) 0 >
    {if}
        (L.L.schaltverzoegerung) 2 > (L.L.schaltverzoegerung_runter) 1 > &&
        {if}
            (L.L.cockpit_throttle) 0.3 max (L.L.engine_n) (C.L.torque_apex_at_RPM) min (F.L.engine_M_maxThrottle) 200 + *
        {else}
            (L.L.cockpit_throttle) 0.3 max (L.L.antrieb_M_restriction) min (L.L.engine_n) (C.L.torque_apex_at_RPM) min (F.L.engine_M_maxThrottle) 200 + *
        {endif}
    {else}
        (L.L.engine_n) (C.L.torque_apex_at_RPM) min (F.L.engine_M_maxThrottle) 200 +
    {endif}
    (L.L.antrieb_M_limit_h) 1 max *
{end}

{macro:antrieb_getr_schaltmoment_runter}
    (L.L.engine_n) (C.L.torque_apex_at_RPM) min (F.L.engine_M_maxThrottle) 200 + (L.L.antrieb_M_limit_h) 1 max *
{end}

{macro:antrieb_getr_ratio}
    (L.L.antrieb_getr_aktugang) s0
    {if}
        l0 1 =
        {if}
            (C.L.antrieb_getr_ratio1)
        {else}
            l0 2 =
            {if}
                (C.L.antrieb_getr_ratio2)
            {else}
                l0 3 =
                {if}
                    (C.L.antrieb_getr_ratio3)
                {else}
                    l0 4 =
                    {if}
                        (C.L.antrieb_getr_ratio4)
                    {else}
                        l0 5 =
                        {if}
                            (C.L.antrieb_getr_ratio5)
                        {else}
                            (C.L.antrieb_getr_ratio6)
                        {endif}
                    {endif}
                {endif}
            {endif}
        {endif}
        (C.L.antrieb_i_achse) * (S.L.antrieb_getr_ratio)
    {endif}
{end}

{macro:antrieb_M_limiter}
    (L.L.antrieb_getr_gangwahl) 1 = (L.L.antrieb_getr_aktugang) 0 = || (C.L.antrieb_engine_drehmomentsperre) ! ||
    {if}
        1 (S.L.antrieb_M_limit_h) (S.L.r11) (S.L.r12) (S.L.r13)
    {else}

        (L.L.antrieb_getr_aktugang) 1 =
        {if}
            (C.L.antrieb_M_limit_c_spd1) (S.L.r10)
            (C.L.antrieb_getr_autoSwUpkickdnSpd1) (S.L.r11)
            (C.L.antrieb_M_limit_l_spd1) (S.L.r12)
        {else}
            (L.L.antrieb_getr_aktugang) 2 =
            {if}
                (C.L.antrieb_M_limit_c_spd2) (S.L.r10)
                (C.L.antrieb_getr_autoSwUpkickdnSpd2) (S.L.r11)
                (C.L.antrieb_M_limit_l_spd2) (S.L.r12)
            {else}
                (L.L.antrieb_getr_aktugang) 3 =
                {if}
                    (C.L.antrieb_M_limit_c_spd3) (S.L.r10)
                    (C.L.antrieb_getr_autoSwUpkickdnSpd3) (S.L.r11)
                    (C.L.antrieb_M_limit_l_spd3) (S.L.r12)
                {else}
                    (L.L.antrieb_getr_aktugang) 4 =
                    {if}
                        (C.L.antrieb_M_limit_c_spd4) (S.L.r10)
                        (C.L.antrieb_getr_autoSwUpkickdnSpd4) (S.L.r11)
                        (C.L.antrieb_M_limit_l_spd4) (S.L.r12)
                    {else}
                        (L.L.antrieb_getr_aktugang) 5 =
                        {if}
                            (C.L.antrieb_M_limit_c_spd5) (S.L.r10)
                            (C.L.antrieb_getr_autoSwUpkickdnSpd5) (S.L.r11)
                            (C.L.antrieb_M_limit_l_spd5) (S.L.r12)
                        {else}
                            (C.L.antrieb_M_limit_c_spd6) (S.L.r10)
                            (C.L.antrieb_getr_autoSwUpkickdnSpd6) (S.L.r11)
                            (C.L.antrieb_M_limit_l_spd6) (S.L.r12)
                        {endif}
                    {endif}
                {endif}
            {endif}
        {endif}
        (L.L.r10) (L.L.torque_inc) + (S.L.antrieb_M_limit_h) (S.L.r13)

        (L.L.antrieb_n_kardanwelle) (L.L.r11) - (S.L.r14) 0 >
        {if}
            (L.L.r14) (L.L.r12) (L.L.r11) - / (S.L.r14)
            (L.L.r13) (L.L.r14) (F.L.antrieb_M_limit_l_g_fest) * (S.L.r11)
            (L.L.r10) (L.L.r14) (F.L.antrieb_M_limit_l_wandler) * (S.L.r12)
            (L.L.r10) (L.L.r14) (F.L.antrieb_M_limit_l_g) * (S.L.r13)
        {else}
            (L.L.r13) (S.L.r11)
            (L.L.r10) (S.L.r12) (S.L.r13)
        {endif}
    {endif}

    (L.L.r11) (S.L.antrieb_M_limit_l_g_fest)
    (L.L.r12) (S.L.antrieb_M_limit_l_wandler)
    (L.L.r13) (S.L.antrieb_M_limit_l_g)
{end}

{macro:antrieb_getr_upshift_constraint}
    (L.L.antrieb_getr_upshift_constraint_override_timer) (S.L.r11)
    (L.L.antrieb_getr_upshift_constraint_override) (S.L.r12)
    (L.L.r10) 0 =
    {if}
        -1 (S.L.r11)
        0 (S.L.r12)
    {else}
        (L.L.r10) -1 =
        {if}
            (L.L.r12) 0 >
            {if}
                (L.L.r11) -1 >=
                {if}
                    -3.5 (S.L.r11)
                {endif}
                (L.L.r11) -2 <
                {if}
                    (L.L.r11) (L.S.Timegap) + -2 min (S.L.r11)
                {endif}
                (L.L.r11) -2 =
                {if}
                    -1 (S.L.r11)
                    0 (S.L.r12)
                {endif}
            {else}
                -1 (S.L.r11)
            {endif}
        {else}
            (L.L.r12) 2 <
            {if}
                (L.L.r11) -1 <=
                {if}
                    (L.L.r12) 0 =
                    {if}
                        2
                    {else}
                        3
                    {endif}
                    (S.L.r11)
                {endif}
                (L.L.r11) 0 >
                {if}
                    (L.L.r11) (L.S.Timegap) - 0 max (S.L.r11)
                {endif}
                (L.L.r11) 0 =
                {if}
                    -1 (S.L.r11)
                    (L.L.r12) 1 + (S.L.r12)
                {endif}
            {else}
                -1 (S.L.r11)
            {endif}
        {endif}
    {endif}
    (L.L.r11) (S.L.antrieb_getr_upshift_constraint_override_timer)
    (L.L.r12) (S.L.antrieb_getr_upshift_constraint_override) (S.L.r10) 0 > (L.L.elec_busbar_main) (C.L.elec_busbar_minV) > &&
        (S.L.antrieb_getr_upshift_constraint_override_warn_sound)
{end}

' Spezieller Fall mit Retarderhebel

{trigger:retarder_aufschalten}
    (L.L.cockpit_retarderhebel) 1 <
    {if}
        (T.L.ev_wischerhebel)
    {endif}

    (L.L.cockpit_retarderhebel) 0.25 + 1 min (S.L.cockpit_retarderhebel)
    (L.L.cockpit_retarderhebel) 0.25 >
    {if}
        (L.L.antrieb_retarderstufe) 1 + 4 min (S.L.antrieb_retarderstufe)
    {endif}
{end}

{trigger:retarder_abschalten}
    (L.L.cockpit_retarderhebel) 0 >
    {if}
        (T.L.ev_wischerhebel)
    {endif}

    (L.L.cockpit_retarderhebel) 0.25 - 0 max (S.L.cockpit_retarderhebel)
    (L.L.antrieb_retarderstufe) 1 - 1 max (S.L.antrieb_retarderstufe)
{end}
