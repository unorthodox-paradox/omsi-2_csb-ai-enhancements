'##############
'Dirt Script
'##############

'Processes Variables to visualize dirt on vehicle and wiped windows

'(c) 10.01.2011 Marcel Kuhnt

'Script Version: 1.0
'Omsi release: 1.0

'Needs:
'- none

'Revision History:
'- Marcel Kuhnt		17.02.2009	Created
'- Marcel Kuhnt		13.06.2009	Moved Wiper Function to wiper script
'- Marcel Kuhnt		10.01.2011	Removed calculation of "dirt_norm" because of rehardcoding of "Dirt_Norm".

'--------------------------------------------------------------------------------------

{trigger:veh_wash}
    (L.L.AI) !
	{if}
	    (L.L.dirt_dirt_norm) 0.015 (L.S.Timegap) * - 0 max (S.L.dirt_dirt_norm)
	{endif}
{end}

{macro:dirt_frame}

	(L.L.dirt_initialized) 0 =
	{if}
		1 (S.L.dirt_initialized)
	{else}
		(L.L.dirt_initialized) 1 =
		{if}
			10000 random (M.V.NrSpecRandom) 0.75 <
			{if}
				0.05 s0 0.1 s1
			{else}
				10000 random (M.V.NrSpecRandom) 0.75 <
				{if}
					0.1 s0 0.15 s1
				{else}
					10000 random (M.V.NrSpecRandom) 0.75 <
					{if}
						0.2 s0 0.3 s1
					{else}
						0.3 s0 0.45 s1
					{endif}
				{endif}
			{endif}
			l0 l1 10000 random (M.V.NrSpecRandom) * + (S.L.dirt_ai_affinity)

			(L.L.AI)
			{if}
				(L.L.dirt_ai_affinity) 0.3 0.7 10000 random (M.V.NrSpecRandom) (L.L.DirtRate) 3600 * * * + 1 min *
			{else}
				0
			{endif}
			(S.L.dirt_dirt_norm)

			2 (S.L.dirt_initialized)
		{else}

'			Dirt incr due to vehicle-local variable

			(L.L.AI)
			{if}
				(L.L.dirt_ai_affinity)
			{else}
				1
			{endif}
			s0

			(L.L.dirt_dirt_norm) (L.L.DirtRate) l0 * (L.L.ai_pre_cockpit_ai_timegap) * 0.1 min + 0 max 1 min (S.L.dirt_dirt_norm)
		{endif}
	{endif}
{end}
